// name: examplary
// keywords: NewBackend
// status: correct

// from OpenModelica Workshop Presentation 2022 by kabdelhak
// used for extreme scaling purposes with slicing

loadString("
model exemplaryS
  parameter Integer s = 20;
  Real x[5+s,2+s];
  Real y[2+s];
  Real z[5+s,2+s] \"reference solution for x\";
  Real e(start = 1, fixed = true) \"accumulated error, start at 1 for tolerance\";
equation
  // this for loop will be split, one part solved for x[i+1,j] and one for x[i,j]
  for i in 1:4+s, j in 1:2+s loop
    x[i+1,j] = x[i,j] - y[j];
  end for;

  // this force splits the upper equation
  for j in 1:2+s loop
    y[j] = j*sin(time);
    x[3,j] = j*cos(time);
  end for;

  // reference value for x (x=z)
  for i in 1:5+s, j in 1:2+s loop
    z[i,j] = j*cos(time) + (3-i)*j*sin(time);
  end for;

  // error accumulation
  der(e) = sqrt(sum((x-z).^2)/(5+s)/(2+s));
end exemplaryS;
"); getErrorString();

setCommandLineOptions("--newBackend");
simulate(exemplaryS); getErrorString();
val(e,1); // should remain close to 1

// Result:
// true
// ""
// true
// record SimulationResult
//     resultFile = "exemplaryS_res.mat",
//     simulationOptions = "startTime = 0.0, stopTime = 1.0, numberOfIntervals = 500, tolerance = 1e-06, method = 'dassl', fileNamePrefix = 'exemplaryS', options = '', outputFormat = 'mat', variableFilter = '.*', cflags = '', simflags = ''",
//     messages = "LOG_SUCCESS       | info    | The initialization finished successfully without homotopy method.
// LOG_SUCCESS       | info    | The simulation finished successfully.
// "
// end SimulationResult;
// ""
// 1.0
// endResult
