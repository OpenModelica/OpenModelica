(*
    Copyright PELAB, Linkoping University

    This file is part of Open Source Modelica (OSM).

    OSM is free software; you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation; either version 2 of the License, or
    (at your option) any later version.

    OSM is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with OpenModelica; if not, write to the Free Software
    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

*)

(**
 ** file:	 daelow.rml
 ** module:      DAELow
 ** description: DAELow a lower form of DAE including sparse matrises for
 ** BLT decomposition, etc.
 **
 ** RCS: %W% %E%
 ** 
 **)

(** - Module header *)

module DAELow:
  
  with "dae.rml"
  with "exp.rml"


  datatype VarKind = VARIABLE | STATE | DUMMY_DER | DUMMY_STATE | DISCRETE | PARAM | CONST

  datatype Var = VAR of Exp.ComponentRef * 
			VarKind * 
			DAE.VarDirection * 
			DAE.Type * 
			Exp.Exp option * (* Binding e.g. for parameters*)
			DAE.InstDims *
			DAE.StartValue *  (* value of start attribute *)
			int (* index in impl. vector *)


  datatype Equation = EQUATION of Exp.Exp * Exp.Exp

  datatype DAELow = DAELOW of Var list (* ordered Variables, only states and alg. vars*)		    
		  * Var list (* Known variables, i.e. constants and parameters*)
	          * Equation list (* ordered Equations *)
	          * Equation list (* Removed equations a=b*)


  type IncidenceMatrix = int list list 

  type IncidenceMatrixT = IncidenceMatrix

  relation dump: DAELow => ()
  relation dump_vars: Var list => ()

  relation lower: (DAE.DAElist) => DAELow
  
  relation incidence_matrix: (DAELow) => IncidenceMatrix
 
  relation transpose_matrix: IncidenceMatrix => IncidenceMatrixT
		  
  relation dump_incidence_matrix: IncidenceMatrix => ()

  relation dump_incidence_matrix_inv: IncidenceMatrixT => ()

  relation dump_matching: (int vector) => () 
	  
  relation matching_algorithm: (DAELow,IncidenceMatrix, IncidenceMatrixT) 
	  => (int vector,(* var solved in eqn*) 
	      int vector, (* eqn solves var *) 
	      DAELow,
	      IncidenceMatrix, 
	      IncidenceMatrixT)

  relation strong_components: (IncidenceMatrix, IncidenceMatrixT,
			       int vector, int vector) => 
	  (int list list )

  relation dump_components: (int list list) => ()

  relation translate_dae: DAELow => DAELow 	  
	  
  relation calculate_sizes: DAELow => (int, (* nx *)
				       int, (* ny *)
				       int) (* np *)
end (* End Module *)

(* Implementation *)
with "util.rml" 
with "daeext.rml"
with "absyn.rml"
with "print.rml"
with "derive.rml"
with "debug.rml" 

relation lower: (DAE.DAElist) => DAELow =

  rule	states(lst) => s &
	(*print "states:" &
	Util.list_map(s,Exp.cref_str) => crefs &
	Util.string_delimit_list(crefs,", " ) => st &
	print st & print "\n" &*)
	lower2(lst,s) => (low as DAELOW(vars,knvars,eqns,reqns))  &
	print "states :" &
	Util.list_map(s,Exp.print_component_ref_str) => sl &
	Util.string_delimit_list(sl,", ") => str & print str & print "\n" &
	remove_simple_equations(low,s) => low' &
	print "removed simple equations, dae:" &
	dump low' 
	(*list_length(vars) => varlen &
	int_string(varlen) => vs &
	list_length(eqns) => eqnlen &
	int_string(eqnlen) => es &
	print vs & print " variables\n========" &
	dump_vars(vars) &
	print st & print "\n" &
	print es & print " equations\n" *)
	--------------------
	lower(lst) => low'
end

relation dump: DAELow => () =

  rule	print "Variables\n" &
	print "=========\n" &
	dump_vars vars &
	print "\n" &
	print "Known Variables (constants)\n" &
	print "=============================\n" &
	dump_vars knvars &
	print "\nEquations\n" &
	print "=========\n" &
	dump_eqns eqns &
	print "Simple Equations\n" &
	print "=========\n" &
	dump_eqns reqns 
	-----------------
	dump(DAELOW(vars,knvars,eqns,reqns))
end
relation dump_eqns: Equation list => () =

  rule	Util.list_map(eqns,equation_str) => lst &
	Util.string_delimit_list(lst,"\n") => s &
	print s & print "\n" 
	----------------
	dump_eqns(eqns)
end

relation equation_str: Equation => string =

  rule	Exp.print_exp_str e1 => s1 &
	Exp.print_exp_str e2 => s2 &
	Util.string_append_list([s1,"=",s2]) => res
	--------------------
	equation_str(EQUATION(e1,e2)) => res
end

(* Move simple equations on the form a=b from equations 2nd in DAELow
 to simple equations 3rd in DAELow to speed up assignment alg. *)
relation remove_simple_equations: (DAELow,Exp.ComponentRef list) 
	=> (DAELow) =

  rule	remove_simple_equations2(eqns,eqns,states,vars,knvars)
	  => (eqns',reqns',vars',knvars')	  
	-------------------------------------
	remove_simple_equations(DAELOW(vars,knvars,eqns,reqns),states) 
	  => DAELOW(vars',knvars',eqns',reqns')
end

relation remove_simple_equations2: (Equation list, (* all equations *)
				    Equation list, (* eqns to iterate*)
				    Exp.ComponentRef list,(* states *)
				    Var list, (* variables states+alg *)
				    Var list) (* known variables *)
	  => (Equation list, (* all equations *)  
	      Equation list, (* removed equations*) 
	      Var list, (* variables states+alg *)
	      Var list) (* known variables *) =

  rule	list_member(cr1,states) => false &
	list_member(cr2,states) => false &
	Util.list_deletemember(alleqns,e) => alleqns' &
	Util.list_map_2(alleqns',replace_all,cr2,cr1) => alleqns'' &
	Util.list_map_2(eqns,replace_all,cr2,cr1) => eqns' &
	remove_variable_named(vars,cr2) => (vars',var) &	
	remove_simple_equations2(alleqns'',eqns',states,vars',var::knvars) 
	  => (alleqns''',reqns,vars'',knvars'')
	--------------------------------------------------
	remove_simple_equations2(alleqns,(e as EQUATION(Exp.CREF(cr1,_),Exp.CREF(cr2,_)))::eqns,states,vars,knvars) 
	  => (alleqns''',e::reqns,vars'',knvars'')
	

  rule	Exp.is_zero(e2) => true &
	list_member(cr1,states) => false &
	list_member(cr2,states) => false &
	Util.list_deletemember(alleqns,e) => alleqns' &	
	Util.list_map_2(alleqns',replace_all,cr2,cr1) => alleqns'' &
	Util.list_map_2(eqns,replace_all,cr2,cr1) => eqns' &
	remove_variable_named(vars,cr2) => (vars',var) &	
	remove_simple_equations2(alleqns'',eqns',states,vars',var::knvars) 
	  => (alleqns''',reqns,vars'',knvars'')
	--------------------------------------------------
	remove_simple_equations2(alleqns,(e as EQUATION(Exp.BINARY(Exp.CREF(cr1,_),Exp.SUB(_),Exp.CREF(cr2,_)),e2))::eqns,states,vars,knvars) 
	  => (alleqns''',e::reqns,vars'',knvars'')

  rule	remove_simple_equations2(alleqns,eqns,states,vars,knvars) 
	  => (alleqns',reqns,vars',knvars')
	--------------------------------------------------------------
	remove_simple_equations2(alleqns,_::eqns,states,vars,knvars)
	  => (alleqns',reqns,vars',knvars')

  axiom	remove_simple_equations2(alleqns,[],states,vars,knvars) 
	  => (alleqns,[],vars,knvars)

end 

relation remove_variable_named: (Var list,  Exp.ComponentRef) => (Var list, Var) =

  rule	Exp.cref_equal(cr1,cr2) => true
	-------------------------------
	remove_variable_named(  (v as VAR(cr1,_,_,_,_,_,_,_))::rest,cr2) => (rest,v)

  rule	remove_variable_named(rest,cr2) => (res,v2)
	-------------------------------
	remove_variable_named(  (v as VAR(cr1,_,_,_,_,_,_,_))::rest,cr2) => (v::res,v2)
end

relation replace_all: (Equation, (* equation *)
		       Exp.ComponentRef, (*source variable, *)
		       Exp.ComponentRef) (*target variable: src replaced with*)
	=> Equation =

  rule	replace_all_exp(e1,s,t) => e1' &
	replace_all_exp(e2,s,t) => e2'
	------------------------------
	replace_all (EQUATION(e1,e2),s,t) => EQUATION(e1',e2')
end

relation replace_all_exp: (Exp.Exp, 
			   Exp.ComponentRef, (* source variable *)
			   Exp.ComponentRef) (* target variable *)
	  => (Exp.Exp) =

  rule	Exp.cref_equal(cr,s) => true
	----------------------------
	replace_all_exp(Exp.CREF(cr,tp),s,t) => Exp.CREF(t,tp)

  rule	replace_all_exp(e1,s,t) => e1' &
	replace_all_exp(e2,s,t) => e2' 
	-----------------------------
	replace_all_exp(Exp.BINARY(e1,op,e2),s,t) => Exp.BINARY(e1',op,e2')

  rule	replace_all_exp(e,s,t) => e' 
	-----------------------------
	replace_all_exp(Exp.UNARY(op,e),s,t) => Exp.UNARY(op,e')

  rule	replace_all_exp(e1,s,t) => e1' &
	replace_all_exp(e2,s,t) => e2' 
	-----------------------------
	replace_all_exp(Exp.LBINARY(e1,op,e2),s,t) => Exp.LBINARY(e1',op,e2')
	
  rule	replace_all_exp(e,s,t) => e'
	-----------------------------
	replace_all_exp(Exp.LUNARY(op,e),s,t) => Exp.LUNARY(op,e')

  rule	replace_all_exp(e1,s,t) => e1' &
	replace_all_exp(e2,s,t) => e2'
	-------------------------
	replace_all_exp(Exp.RELATION(e1,rel,e2),s,t) => Exp.RELATION(e1',rel,e2')
	
  rule	replace_all_exp(e1,s,t) => e1' &
	replace_all_exp(e2,s,t) => e2' &
	replace_all_exp(e3,s,t) => e3'
	-------------------------
	replace_all_exp(Exp.IFEXP(e1,e2,e3),s,t) => Exp.IFEXP(e1',e2',e3')

  rule	Util.list_map_2(expl,replace_all_exp,s,t) => expl' 
	-------------------------------------------------
	replace_all_exp(Exp.CALL(a,expl,b,c),s,t) => Exp.CALL(a,expl',b,c)
	
  rule	Util.list_map_2(expl,replace_all_exp,s,t) => expl' 
	--------------------------------------------
	replace_all_exp(Exp.ARRAY(tp,b,expl),s,t) => Exp.ARRAY(tp,b,expl')

  rule	Print.print_buf "replace_all_exp on MATRIX not impl.\n" &
	print "replace_all_exp on MATRIX not impl.\n"
	---------------------------------------
	replace_all_exp(e as Exp.MATRIX(_,_,_),s,t) => e
	
  rule	Util.list_map_2(expl,replace_all_exp,s,t) => expl'
	-----------------------
	replace_all_exp(Exp.TUPLE(expl),s,t) => Exp.TUPLE(expl')

  rule	replace_all_exp(e,s,t) => e'
	-----------------------
	replace_all_exp(Exp.CAST(tp,e),s,t) => Exp.CAST(tp,e')

  rule	replace_all_exp(e,s,t) => e'
	----------------------------
	replace_all_exp(Exp.ASUB(e,i),s,t) => Exp.ASUB(e,i)

  rule	replace_all_exp(e1,s,t) => e1' &
	replace_all_exp(e2,s,t) => e2' 
	------------------------------
	replace_all_exp(Exp.REDUCTION(a,e1,b,e2),s,t) 
	  => Exp.REDUCTION(a,e1',b,e2')

  axiom	replace_all_exp(e,s,t) => e
end


relation dump_vars: Var list => ()=

  axiom	dump_vars [] => ()

  rule	Exp.cref_str(cr) => str &
	print str & print ":" & dump_kind kind &
	print ", " &
	dump_vars(xs) 
	-------------
	dump_vars(VAR(cr,kind,_,_,_,_,_,_)::xs)
end 
relation dump_kind: VarKind => () =

  rule	print "VARIABLE" 
	--------------
	dump_kind VARIABLE

  rule	print "STATE" 
	--------------
	dump_kind STATE

  rule	print "DUMMY_DER" 
	--------------
	dump_kind DUMMY_DER

  rule	print "DUMMY_STATE" 
	--------------
	dump_kind DUMMY_STATE

  rule	print "DISCRETE" 
	--------------
	dump_kind DISCRETE

  rule	print "PARAM" 
	--------------
	dump_kind PARAM

  rule	print "CONST" 
	--------------
	dump_kind CONST
end

relation states: (DAE.DAElist) => Exp.ComponentRef list =

  axiom states DAE.DAE([]) => []

  rule	states(DAE.DAE(xs)) => s &
	states_exp(e1) => s1 &
	states_exp(e2) => s2 &
	Util.list_list_union_p([s,s1,s2],Exp.cref_equal) => res
	-----------------------------------
	states(DAE.DAE(DAE.EQUATION(e1,e2)::xs))  => res

  rule	states(dae) => s1 &
	states(DAE.DAE(xs)) => s2 &
	Util.list_union_p(s1,s2,Exp.cref_equal) => res
	-------------------------
	states(DAE.DAE(DAE.COMP(_,dae)::xs)) => res
	
  rule  states(DAE.DAE(xs)) => res
	-----------------
	states(DAE.DAE(_::xs)) => res
end

relation states_exp: (Exp.Exp) => Exp.ComponentRef list =

  rule	states_exp(e1) => s1 &
	states_exp(e2) => s2 &
	Util.list_union_p(s1,s2,Exp.cref_equal) => res
	-----------------------------
	states_exp(Exp.BINARY(e1,_,e2)) => res

  rule	states_exp(e) => res
	-----------------------------
	states_exp(Exp.UNARY(_,e)) => res

  rule	states_exp(e1) => s1 &
	states_exp(e2) => s2 &
	Util.list_union_p(s1,s2,Exp.cref_equal) => res
	-----------------------------
	states_exp(Exp.LBINARY(e1,_,e2)) => res
	
  rule	states_exp(e) => res
	-----------------------------
	states_exp(Exp.LUNARY(_,e)) => res

  rule	states_exp(e1) => s1 &
	states_exp(e2) => s2 &
	Util.list_union_p(s1,s2,Exp.cref_equal) => res
	-------------------------
	states_exp(Exp.RELATION(e1,_,e2)) => res
	
  rule	states_exp(e1) => s1 &
	states_exp(e2) => s2 &
	states_exp(e3) => s3 &
	Util.list_list_union_p([s1,s2,s3],Exp.cref_equal) => res
	-------------------------
	states_exp(Exp.IFEXP(e1,e2,e3)) => res

  axiom	states_exp(Exp.CALL(Absyn.IDENT("der"),[Exp.CREF(cr,_)],_,_)) => [cr]

  rule	Util.list_map(expl,states_exp) => lst &
	Util.list_list_union_p(lst,Exp.cref_equal) => res
	------------------------
	states_exp(Exp.CALL(_,expl,_,_)) => res
	
  rule	Util.list_map(expl,states_exp) => lst &
	Util.list_list_union_p(lst,Exp.cref_equal) => res
	-----------------------
	states_exp(Exp.ARRAY(_,_,expl)) => res

  rule	Print.print_buf "states_exp on MATRIX not impl.\n" &
	print "states_exp on MATRIX not impl.\n"
	---------------------------------------
	states_exp(Exp.MATRIX(_,_,_)) => []
	
  rule	Util.list_map(expl,states_exp) => lst &
	Util.list_list_union_p(lst,Exp.cref_equal) => res
	-----------------------
	states_exp(Exp.TUPLE(expl)) => res

  rule	states_exp(e) => res
	-----------------------
	states_exp(Exp.CAST(_,e)) => res

  rule	states_exp(e) => res
	-----------------------
	states_exp(Exp.ASUB(e,_)) => res

  rule	states_exp(e1) => s1 &
	states_exp(e2) => s2 &
	Util.list_union_p(s1,s2,Exp.cref_equal) => res
	-------------------------
	states_exp(Exp.REDUCTION(_,e1,_,e2)) => res

  axiom	states_exp(_) => []
end


relation lower2: (DAE.DAElist, Exp.ComponentRef list) => DAELow =
	
  axiom	lower2 (DAE.DAE([]),_) => DAELOW([],[],[],[])
	
  rule	lower2(DAE.DAE(xs),states) => DAELOW(vars,knvars,eqns,reqns) &
	lower_var (v,states) => v'
	--------------------------------
	lower2 (DAE.DAE((v as DAE.VAR(_,_,_,_, _,_,_,_))::xs),states) 
	  => DAELOW(v'::vars,knvars,eqns,reqns)

  rule	lower2(DAE.DAE(xs),states) => DAELOW(vars,knvars,eqns,reqns) &
	lower_known_var (v) => v'
	--------------------------------
	lower2 (DAE.DAE((v as DAE.VAR(_,_,_,_, _,_,_,_))::xs),states) 
	  => DAELOW(vars,v'::knvars,eqns,reqns)
	  
  rule	lower2(DAE.DAE(xs),states) => DAELOW(vars,knvars,eqns,reqns) &
	lower_eqn(e) => e' 
	--------------------------------
	lower2 (DAE.DAE((e as DAE.EQUATION(e1,e2))::xs),states) 
	  => DAELOW(vars,knvars,e'::eqns,reqns)

  rule	lower2(dae,states) => DAELOW(vars1,knvars1,eqns1,reqns1) &
	lower2(DAE.DAE(xs),states) => DAELOW(vars2,knvars2,eqns2,reqns2) &
	list_append(vars1,vars2) => vars &
	list_append(eqns1,eqns2) => eqns &
	list_append(reqns1,reqns2) => reqns &
	list_append(knvars1,knvars2) => knvars
	--------------------------------
	lower2(DAE.DAE(DAE.COMP(_,dae)::xs),states) 
	  => DAELOW(vars,knvars,eqns,reqns)
	
  rule	lower2(DAE.DAE(xs),states) => res
	---------------------
	lower2 (DAE.DAE(_::xs),states) => res	

  rule	print "-lower2 failed\n" 
	------------------------
	lower2 (_,_) => fail	
end

relation lower_eqn: (DAE.Element) => Equation =

  rule	Exp.simplify(e1) => e1' &
	Exp.simplify(e2) => e2'
	-----------------------
	lower_eqn(DAE.EQUATION(e1,e2)) => EQUATION(e1',e2')
end

relation lower_var: (DAE.Element, Exp.ComponentRef list) => Var =

  rule	lower_varkind(kind,name,states) => kind'
	----------------------------------------
	lower_var (DAE.VAR(name,kind,dir,tp,bind,dims,start,_),states)
	  => VAR(name,kind',dir,tp,bind,dims,start,0)

(*  rule	print "-lower_var failed\n" 
	------------------------
	lower_var (_,_) => fail*)

end

relation lower_known_var: (DAE.Element) => Var =

  rule	lower_known_varkind(kind) => kind'
	----------------------------------------
	lower_known_var (DAE.VAR(name,kind,dir,tp,bind,dims,start,_))
	  => VAR(name,kind',dir,tp,bind,dims,start,0)
end

relation lower_varkind: (DAE.VarKind, Exp.ComponentRef, Exp.ComponentRef list) 
	=> VarKind =
	(* Fails for not states that are not algebraic variables, e.g.
	 parameters and constants *)

  rule	list_member(v,states) => true
	-----------------------------
	lower_varkind(DAE.VARIABLE,v,states) => STATE

  axiom	lower_varkind(DAE.VARIABLE,_,_) => VARIABLE

end

relation lower_known_varkind: (DAE.VarKind) => VarKind =
(* Fails for everything but parameters and constants *)

  axiom	lower_known_varkind(DAE.PARAM) => PARAM
(*  axiom	lower_known_varkind(DAE.CONST) => CONST*)
end


relation varnames: Var list => Exp.ComponentRef list =

  axiom	varnames [] => []

  rule	varnames(vs) => res
	-----------------
	varnames (VAR(cr,_,_,_, _,_,_,_)::vs) => cr::res
end


relation incidence_matrix: (DAELow)
	  => IncidenceMatrix =
	  
  axiom	incidence_matrix(DAELOW(_,_,[],_)) => [] 

  rule	incidence_matrix(DAELOW(vars,knvars,eqns,reqns)) => lst &
	incidence_row(vars,e) => row
	----------------------------
	incidence_matrix(DAELOW(vars,knvars,e::eqns,reqns)) => row::lst

  rule	print "incidence_matrix failed\n" 
	----------------------
	incidence_matrix(_) => fail
end


relation incidence_row: ( Var list, Equation) 
	  => int list =
	
  rule	(*Inefficient to for each cref in equation traverse the varlist*)
	(* Should use a map or a bintree*)
	incidence_row_exp(e1,vars) => lst1 &
	incidence_row_exp(e2,vars) => lst2 &
	list_append(lst1,lst2) => res
	------------------------------------
	incidence_row(vars,EQUATION(e1,e2)) => res
end 

relation incidence_row_exp: (Exp.Exp, Var list ) 
	  => int list =

  rule	(* If variable x is a state, der(x) is a variable in incidence matrix,
	 but not x itself, it is considered known.*)
	get_var(cr,vars) => (VAR(_,STATE,_,_ ,_,_,_,_),_)
 	-----------------------------
	incidence_row_exp(Exp.CREF(cr,_),vars) => []
	
  rule	get_var(cr,vars) => (VAR(_,VARIABLE,_,_, _,_,_,_),p) 
	-----------------------------
	incidence_row_exp(Exp.CREF(cr,_),vars) => [p]

  rule	get_var(cr,vars) => (VAR(_,DUMMY_DER,_,_ ,_,_,_,_),p) 
	-----------------------------
	incidence_row_exp(Exp.CREF(cr,_),vars) => [p]

  rule	get_var(cr,vars) => (VAR(_,DUMMY_STATE,_,_, _,_,_,_),p) 
	-----------------------------
	incidence_row_exp(Exp.CREF(cr,_),vars) => [p]

  rule	incidence_row_exp(e1,vars) => s1 &
	incidence_row_exp(e2,vars) => s2 &
	list_append(s1,s2) => res
	-----------------------------
	incidence_row_exp(Exp.BINARY(e1,_,e2),vars) => res

  rule	incidence_row_exp(e,vars) => res
	-----------------------------
	incidence_row_exp(Exp.UNARY(_,e),vars) => res

  rule	incidence_row_exp(e1,vars) => s1 &
	incidence_row_exp(e2,vars) => s2 &
	list_append(s1,s2) => res
	-----------------------------
	incidence_row_exp(Exp.LBINARY(e1,_,e2),vars) => res
	
  rule	incidence_row_exp(e,vars) => res
	-----------------------------
	incidence_row_exp(Exp.LUNARY(_,e),vars) => res

  rule	incidence_row_exp(e1,vars) => s1 &
	incidence_row_exp(e2,vars) => s2 &
	list_append(s1,s2) => res
	-------------------------
	incidence_row_exp(Exp.RELATION(e1,_,e2),vars) => res
	
  rule	incidence_row_exp(e1,vars) => s1 &
	incidence_row_exp(e2,vars) => s2 &
	incidence_row_exp(e3,vars) => s3 &
	Util.list_flatten([s1,s2,s3]) => res
	-------------------------
	incidence_row_exp(Exp.IFEXP(e1,e2,e3),vars) => res

  rule  get_var(cr,vars) => (VAR(_,STATE,_,_, _,_,_,_),p)
	--------------------------------
	incidence_row_exp(Exp.CALL(Absyn.IDENT("der"),[Exp.CREF(cr,_)],_,_),vars) 
	  => [p]

  rule  get_var(cr,vars) => (_,p)
	--------------------------------
	incidence_row_exp(Exp.CALL(Absyn.IDENT("der"),[Exp.CREF(cr,_)],_,_),vars) 
	  => []

  rule	Util.list_map_1(expl,incidence_row_exp,vars) => lst &
	Util.list_flatten(lst) => res
	-----------------------------------------------
	incidence_row_exp(Exp.CALL(_,expl,_,_),vars) => res
	
  rule	print "not impl. yet" 
	-----------------------
	incidence_row_exp(Exp.ARRAY(_,_,expl),vars) => []

  rule	Print.print_buf "incidence_row_exp on MATRIX not impl.\n" &
	print "incidence_row_exp on MATRIX not impl.\n"
	---------------------------------------
	incidence_row_exp(Exp.MATRIX(_,_,_),vars) => []
	
  rule	print "not impl. yet." 
	-----------------------
	incidence_row_exp(Exp.TUPLE(expl),vars) => []

  rule	incidence_row_exp(e,vars) => res
	-----------------------
	incidence_row_exp(Exp.CAST(_,e),vars) => res

  rule	incidence_row_exp(e,vars) => res
	-----------------------
	incidence_row_exp(Exp.ASUB(e,_),vars) => res

  rule	incidence_row_exp(e1,vars) => s1 &
	incidence_row_exp(e2,vars) => s2 &
	list_append(s1,s2) => res
	--------------------
	incidence_row_exp(Exp.REDUCTION(_,e1,_,e2),vars) => res

  axiom	incidence_row_exp(_,_) => []
end


relation get_var: (Exp.ComponentRef, Var list) => (Var,int) =
	
  rule	Exp.cref_equal(cr,cr2) => true
	-----------------------------
	get_var(cr,(v as VAR(cr2,_,_,_ ,_,_,_,_))::vars) => (v,1)
	
  rule	get_var(cr,vars) => (v,pos) &
	int_add(pos,1) => pos'
	-----------------------------
	get_var(cr,v as VAR(cr2,_,_,_, _,_,_,_)::vars) => (v,pos')
end

relation transpose_matrix: IncidenceMatrix => IncidenceMatrixT =

  rule	list_length(m) => neq &
	transpose_matrix2(m,neq,0,[]) => mt
	----------------
	transpose_matrix(m) => mt
end
relation transpose_matrix2: (IncidenceMatrix,int,int,IncidenceMatrixT) 
	  => IncidenceMatrixT =
  axiom	transpose_matrix2(_,0,_,_) => []

  rule	int_sub(neq,1) => neq' &
	int_add(eqno,1) => eqno' &
	transpose_matrix2(m,neq',eqno',mt) => mt'&
	transpose_row(m,eqno',1) => row
	-------------------------
	transpose_matrix2(m,neq,eqno,mt) => row::mt'
end

relation transpose_row: (IncidenceMatrix,int,int) => int list =

  axiom	transpose_row([],_,_) => []
	
  rule	list_member(eqn,m) => true &
	int_add(varno,1) => varno' &
	transpose_row(ms,eqn,varno') => res
	---------------------------
	transpose_row(m::ms,eqn,varno) => varno::res

  rule	list_member(eqn,m) => false &
	int_add(varno,1) => varno' &
	transpose_row(ms,eqn,varno') => res
	---------------------------
	transpose_row(m::ms,eqn,varno) => res
end

relation dump_incidence_matrix: IncidenceMatrix => () =

  rule	print "Incidence Matrix\n (row == equation)" &
	print "====================================\n" &
	dump_incidence_matrix2(m) 
	-------------------------
	dump_incidence_matrix(m) => ()
end 

relation dump_incidence_matrix_inv: IncidenceMatrix => () =

  rule	print "Transpose Incidence Matrix (row == var)\n" &
	print "=====================================\n" &
	dump_incidence_matrix2(m) 
	-------------------------
	dump_incidence_matrix_inv(m) => ()
end 
	
relation dump_incidence_matrix2: IncidenceMatrix => () =

  axiom	dump_incidence_matrix2([]) => ()

  rule	dump_incidence_row(row) &
	dump_incidence_matrix2(rows)
	---------------------------
	dump_incidence_matrix2(row::rows)
end

relation dump_incidence_row: int list => () =

  rule	print "\n"
	----------
	dump_incidence_row [] 

  rule	int_string(x) => s &
	print s & print " " &
	dump_incidence_row(xs) 
	----------------------
	dump_incidence_row (x::xs)
end

relation dump_matching: (int vector) => () =
	
  rule	print "Matching\n" &
	print "========\n" &
	dump_matching2(v,0) 
	-------------------
	dump_matching(v) 
end

relation dump_matching2: (int vector, int) => () =

  rule	vector_length(v) => len &
	int_add(i,1) => i' &
	int_eq(len,i') => true &
	int_string i' => s & 
	print "var " & print s & 
	print " is solved in eqn " &
	vector_nth(v,i) => eqn &
	int_string eqn => s2 & print s2 & print "\n" 
	---------------------
	dump_matching2(v,i) 

  rule	vector_length(v) => len &
	int_add(i,1) => i' &
	int_eq(len,i') => false &
	int_string i' => s & 
	print "var " & print s & 
	print " is solved in eqn " &
	vector_nth(v,i) => eqn &
	int_string eqn => s2 & print s2 & print "\n" &
	dump_matching2(v,i') 
	---------------------
	dump_matching2(v,i) 
end

relation matching_algorithm: (DAELow,IncidenceMatrix, IncidenceMatrixT) 
	  => ( int vector , int vector,
	      DAELow,IncidenceMatrix, IncidenceMatrixT) =

  rule	list_length(m) => nvars &
	list_length(mt) => neqns &
	Util.list_fill(0,nvars) => assign1 &
	Util.list_fill(0,nvars) => assign2 &
	matching_algorithm2(dae,m,mt,nvars,neqns,1,assign1,assign2) 
	  => (lst1, lst2,dae,m,mt) &
	list_vector(lst1) => vec1 &
	list_vector(lst2) => vec2 
	----------------------
	matching_algorithm(dae,m,mt) => (vec1,vec2,dae,m,mt)
end

relation matching_algorithm2: (DAELow,IncidenceMatrix, IncidenceMatrixT
			       ,int (* number of vars*)
			       ,int (* number of eqns*)
			       ,int (* current var*)
			       , int list, int list) 
	  =>  (int list, 
	       int list,
	       DAELow,
	       IncidenceMatrix, 
	       IncidenceMatrixT) =
	  
  rule	int_eq(nv,i) => true &
	DAEEXT.init_marks(nv,nf) &(* eMark(i)=vMark(i)=false *)
	path_found(m,mt,i,ass1,ass2) => (ass1',ass2')  (* exit loop *)
	---------------------------------------------
	matching_algorithm2(dae,m,mt,nv,nf,i,ass1,ass2) 
	  => (ass1',ass2',dae,m,mt)

  rule	int_add(i,1) => i' &
	DAEEXT.init_marks(nv,nf) &(* eMark(i)=vMark(i)=false *)
	path_found(m,mt,i,ass1,ass2) => (ass1',ass2') & 
	matching_algorithm2(dae,m,mt,nv,nf,i',ass1',ass2') 
	  => (ass1'',ass2'',dae,m,mt)
	----------------------------------------------------------------
	matching_algorithm2(dae,m,mt,nv,nf,i,ass1,ass2) 
	  => (ass1'',ass2'',dae,m,mt) 

  rule	(* path_found failed, Try index reduction using dummy derivatives.
	 When a constraint exist between states and index reduction is needed
	 the dummy derivative will select one of the states as a dummy state
	 (and the derivative of that state as a dummy derivative).
	 For instance, u1=u2 is a constraint between states. Choose u1 as dummy state
	 and der(u1) as dummy derivative, named der_u1. The differentiated function 
	 then becomes: der_u1 = der(u2).
	 In the dummy derivative method this equation is added and the original equation
	 u1=u2 is kept. This is not the case for the original pantilides algorithm, where
	 the original equation is removed from the system.
	 *)
	reduce_index_dummy_der(dae,m,mt,nv,nf,i) 
	  => (dae as DAELOW(vars,knvars,eqns,_),m,mt) &
(*	  print "reduced index, new DAE:\n" &
	dump dae &*)
	  (* and try again, restarting. This could be optimized later. It should not
	   be necessary to restart the matching. Instead one could continue the matching
	   as usual. This has not been tested yet.*)
	list_length(vars) => nv' &
	list_length(eqns) => nf' &
	Util.list_fill(0,nv') => ass1 &
	Util.list_fill(0,nf') => ass2 &
	matching_algorithm2(dae,m,mt,nv',nf',1,ass1,ass2)
	  => (ass1',ass2',dae,m,mt)
	------------------------
	matching_algorithm2(dae,m,mt,nv,nf,i,ass1,ass2) 
	  => (ass1',ass2',dae,m,mt)

  rule	print "Error, model singular. i=" &
	int_string(i) => is & print is & print "\n" 
	& DAEEXT.dump_marked_equations(nf) 
	& DAEEXT.dump_marked_variables(nv)
	------------------------------
 	matching_algorithm2(dae,m,mt,nv,nf,i,ass1,ass2) => fail
end

relation reduce_index_dummy_der: (DAELow, 
				  IncidenceMatrix, 
				  IncidenceMatrixT,
				  int, (* number of vars*)
				  int, (* number of eqns*)
				  int) (* i *)
			    
			    => (DAELow,
				IncidenceMatrix, 
				IncidenceMatrixT) =
	
  rule	marked_eqns(nf) => eqns &

	Util.list_map(eqns,int_string) => es &
	Util.string_delimit_list(es,", ") => es' &
	print "marked equations " & print es' & print "\n" &
	(* Collect the states in the equations that are singular, i.e. composing
	 a constraint between states.*)
	states_in_eqns(eqns,dae,m,mt) => (states,stateindx) &
	differentiate_eqns(dae,m,mt,nv,nf,eqns) 
	  => (dae,m,mt,nv,nf,deqns) &	
	  update_incidence_matrix(dae,m,mt,i) => (m,mt) &
	select_dummy_state(states,stateindx,dae,m,mt) => (state,stateno) & 
	new_dummy_var(state,dae) => (dummy_der,dae) &
	eqns_for_var(mt,stateno) => reqns &
	Util.list_union_p(reqns,deqns,int_eq) => changedeqns &
	(* We need to change variables in the differentiated equations and in the 
	  equations having the dummy derivative *)
	replace_dummy_der(state,dummy_der,dae,m,mt,changedeqns) => (dae,m,mt) &
	make_algebraic(dae,state) => dae &
	update_incidence_matrix(dae,m,mt,i) => (m,mt) 
	---------------------------------------------
	reduce_index_dummy_der(dae,m,mt,nv,nf,i) 
	  => (dae,m,mt)

  rule	marked_eqns(nf) => eqns &

	Util.list_map(eqns,int_string) => es &
	Util.string_delimit_list(es,", ") => es' &
	states_in_eqns(eqns,dae,m,mt) => ([],_) &
	print_equations(eqns,dae) 
	---------------------------------------------
	reduce_index_dummy_der(dae,m,mt,nv,nf,i) 
	  => fail

  rule	print "-reduce_index_dummy_der failed\n"
	--------------------
	reduce_index_dummy_der(_,_,_,_,_,_) => fail
end

relation update_incidence_matrix:(DAELow,
				  IncidenceMatrix, 
				  IncidenceMatrixT,
				  int) (* i *) 
	  => (IncidenceMatrix, 
	      IncidenceMatrixT) =

  rule	(* Inefficient, should only update the added differentiated variables *)
	incidence_matrix(dae) => m' &
	transpose_matrix(m') => mt'
	---------------------
	update_incidence_matrix(dae,m,mt,i) => (m',mt')
end

(* Make the variable a dummy derivative, i.e. change varkind from STATE to DUMMY_STATE *)
relation make_algebraic: (DAELow, 
			  Exp.ComponentRef) (* state *)
	  => (DAELow) =

  axiom	make_algebraic(DAELOW([],[],e,es),_) => DAELOW([],[],e,es)
	
  rule	Exp.cref_equal(cr,state) => true 
	--------------------------------
	make_algebraic(DAELOW((v as VAR(cr,kind,d,t,b,dim,start,idx))::vs,kv,e,se),state)
	  =>	(DAELOW(VAR(cr,DUMMY_STATE,d,t,b,dim,start,idx)::vs,kv,e,se))
	  
  rule	make_algebraic(DAELOW(vs,kv,e,se),state) => DAELOW(vs',kv',e',se')
	-----------------------------------------------------------
	make_algebraic(DAELOW(v::vs,kv,e,se),state)
	  => (DAELOW(v::vs',kv',e',se'))
end	  

(* replaces der(state) with the variable dummy der *)
relation replace_dummy_der: (Exp.ComponentRef, (* state *)
			     Exp.ComponentRef, (* dummy der name *)
			     DAELow, 
			     IncidenceMatrix, 
			     IncidenceMatrixT,
			     int list) 
	  => ( DAELow, 
	      IncidenceMatrix, 
	      IncidenceMatrixT) =

  axiom	replace_dummy_der(state,dummy,dae,m,mt,[]) => (dae,m,mt)

  rule	int_sub(e,1) => e' &
	list_nth(eqns,e') => eqn &
	replace_dummy_der2(state,dummyder,eqn) => eqn' &
	replace_dummy_der_others(v,eqn') => (eqn',v') &
	Util.list_union_p(v,v',var_equal) => v'' &
	Util.list_replaceat(eqn',e',eqns) => eqns' &
	replace_dummy_der(state,dummyder,DAELOW(v'',kv,eqns',seqns),m,mt,rest) 
	  => (dae,m,mt) 
	-------------------------------
	replace_dummy_der(state,dummyder,DAELOW(v,kv,eqns,seqns),m,mt,e::rest)
	  => (dae,m,mt)

  rule	print "-replace_dummy_der failed\n" 
	-------------------
	replace_dummy_der(_,_,_,_,_,_) => fail
end

(* replaces der(state) with dummy_der variable in equationlist_append *)
relation replace_dummy_der2:(Exp.ComponentRef,
			     Exp.ComponentRef,
			     Equation) => Equation =

  rule	let dercall = Exp.CALL(Absyn.IDENT("der"),
			       [Exp.CREF(st,Exp.REAL)],
			       false,
			       false) &
	Exp.replace_exp(e1,dercall, Exp.CREF(dummyder,Exp.REAL)) => (e1',_) &
	Exp.replace_exp(e2,dercall, Exp.CREF(dummyder,Exp.REAL)) => (e2',_)
	---------------------------------------------------------------
	replace_dummy_der2(st,dummyder,EQUATION(e1,e2)) => EQUATION(e1',e2')
	
  rule	print "-replace_dummy_der2 failed\n" 
	---------------
	replace_dummy_der2(_,_,_) => fail
end
(* Replaces 
 1. der(der_s)  with der2_s
 2. der(der(v)) with der2_v
 3. der(v)  for alg. var v with der_v 
 *)
relation replace_dummy_der_others:( Var list ,Equation) => (Equation,Var list) =

  axiom	replace_dummy_der_others([],eqn) => (eqn,[])

  rule	(* der(der_c) => der_der_c *)
	let dercall = Exp.CALL(Absyn.IDENT("der"),
			       [Exp.CREF(cr,Exp.REAL)],
			       false,
			       false) &
	create_dummy_var(cr) => dummyder &
	
	Exp.replace_exp(e1,dercall, Exp.CREF(dummyder,Exp.REAL)) => (e1',c1) &
	Exp.replace_exp(e2,dercall, Exp.CREF(dummyder,Exp.REAL)) => (e2',c2) &
	int_add(c1,c2) => c &
	int_gt(c,0) => cond &
	Util.if(cond,[VAR(dummyder,DUMMY_DER,a,b,NONE,d,NONE,0)],[]) => varlst &
	replace_dummy_der_others(vs,EQUATION(e1',e2')) => (res,allvars) &
	Util.list_union_p(allvars,varlst,var_equal) => allvars'
	---------------------------------------------------------------
	replace_dummy_der_others(VAR(cr,DUMMY_DER,a,b,c,d,e,f)::vs,EQUATION(e1,e2))
	  => (res,allvars')

  rule	(* der(der(s)) s is state => der_der_s) *)
	let dercall = Exp.CALL(Absyn.IDENT("der"),
			       [Exp.CALL(Absyn.IDENT("der"),
					 [Exp.CREF(cr,Exp.REAL)],
					  false,
					  false)],
			       false,
			       false) &
	create_dummy_var(cr) => dummyder & 
	create_dummy_var(dummyder) => dummyder' &
	Exp.replace_exp(e1,dercall, Exp.CREF(dummyder',Exp.REAL)) => (e1',c1) &
	Exp.replace_exp(e2,dercall, Exp.CREF(dummyder',Exp.REAL)) => (e2',c2) &
	int_add(c1,c2) => c &
	int_gt(c,0) => cond &
	Util.if(cond,[VAR(dummyder',DUMMY_DER,a,b,NONE,d,NONE,0)],[]) => varlst &
	replace_dummy_der_others(vs,EQUATION(e1',e2')) => (res,allvars) &
	Util.list_union_p(allvars,varlst,var_equal) => allvars'
	---------------------------------------------------------------------------
	replace_dummy_der_others(VAR(cr,STATE,a,b,c,d,e,f)::vs,EQUATION(e1,e2))
	  => (res,allvars')

  rule	(* der(v) v is alg var => der_v *)
	let dercall = Exp.CALL(Absyn.IDENT("der"),
					 [Exp.CREF(cr,Exp.REAL)],
					  false,
					  false) &
	create_dummy_var(cr) => dummyder & 
	create_dummy_var(dummyder) => dummyder' &
	Exp.replace_exp(e1,dercall, Exp.CREF(dummyder',Exp.REAL)) => (e1',c1) &
	Exp.replace_exp(e2,dercall, Exp.CREF(dummyder',Exp.REAL)) => (e2',c2) &
	int_add(c1,c2) => c &
	int_gt(c,0) => cond &
	Util.if(cond,[VAR(dummyder',DUMMY_DER,a,b,NONE,d,NONE,0)],[]) => varlst &
	replace_dummy_der_others(vs,EQUATION(e1',e2')) => (res,allvars) &
	Util.list_union_p(allvars,varlst,var_equal) => allvars'
	---------------------------------------------------------------------------
	replace_dummy_der_others(VAR(cr,VARIABLE,a,b,c,d,e,f)::vs,EQUATION(e1,e2))
	  => (res,allvars')

  rule	replace_dummy_der_others(vs,EQUATION(e1,e2)) => (res,allvars)
	----------------------------------------------------------
	replace_dummy_der_others(_::vs,EQUATION(e1,e2))
	  => (res,allvars)
end

relation var_equal: (Var,Var) => bool =

  rule	Exp.cref_equal(cr1,cr2) => res 
	------------------------------
	var_equal(VAR(cr1,_,_,_ ,_,_,_,_),VAR(cr2,_,_,_ ,_,_,_,_)) => res
end

(* creates a new variable named "der_"+<varname> and adds it to the dae *) 
relation new_dummy_var:(Exp.ComponentRef,DAELow) => (Exp.ComponentRef,DAELow) =

  rule	get_var(var,vars) => (VAR(_,kind,dir,tp,bind,dim,start,idx),_) &
	create_dummy_var(var) => dummyvar &
	list_append(vars,[VAR(dummyvar,DUMMY_DER,dir,tp,NONE,dim,NONE,0)]) 
	  => vars'
	-----------------------------------
	new_dummy_var(var,DAELOW(vars,kv,eqns,seqns)) 
	  => (dummyvar, DAELOW(vars',kv,eqns,seqns))
end

relation create_dummy_var: (Exp.ComponentRef) => Exp.ComponentRef =

  rule	
	string_append("der_",id) => id'
	-------------------------------
	create_dummy_var(Exp.CREF_IDENT(id,subs)) 
	  => Exp.CREF_IDENT(id',subs)
	  
  rule	create_dummy_var(cr) => cr'
	------------------------
	create_dummy_var(Exp.CREF_QUAL(id,subs,cr)) 
	  => Exp.CREF_QUAL(id,subs,cr')

end

(* Heuristic to select among the states which one will be transformed into
 an algebraic variable, a so called dummy state (dummy derivative) *)
relation select_dummy_state: (Exp.ComponentRef list, (* variable names *)
			      int list, (* variable numbers *)
			      DAELow, 
			      IncidenceMatrix,
			      IncidenceMatrixT) 
	  => (Exp.ComponentRef, int) =
	  
	  (* for now, select the first one... *)
  axiom	select_dummy_state(s::_,sn::_,_,_,_) => (s,sn)

  rule	print "Error, no state to select\n" 
	------------------------------------
	select_dummy_state([],_,_,_,_) => fail
end
	
relation states_in_eqns: (int list (* eqns *),
			  DAELow, 
			  IncidenceMatrix,
			  IncidenceMatrixT) 
	  => (Exp.ComponentRef list, (* name for each state *)
	      int list)  (* number for each state *)
	=

  axiom states_in_eqns([],_,_,_) => ([],[])

  rule	states_in_eqns(rest,DAELOW(vars,kv,eqns,seqns),m,mt) => (res1,res2) &
	int_sub(e,1) => e' &
	list_nth(eqns,e') => eqn &
	states_in_eqn(eqn,vars) => vars2 &
	states_in_vars(vars,vars2) => (res11,res22) &
	list_append(res11,res1) => res1' &
	list_append(res22,res2) => res2'
	--------------------------------
	states_in_eqns(e::rest,DAELOW(vars,kv,eqns,seqns),m,mt) => (res1',res2')

  rule	print "-states_in_eqns failed\n"
	-------------------------
	states_in_eqns(_,_,_,_) => fail
	
end

relation states_in_vars: (Var list, int list) => 
	(Exp.ComponentRef list, (* names of the states *)
	 int list ) (* number for each state *)  =

  axiom	states_in_vars (vars,[]) => ([],[])

  rule	int_sub(v,1) => v' &
	list_nth(vars,v') => VAR(cr,_,_,_,_,_,_,_) &
	states_in_vars(vars,rest) => (res1,res2)  
	---------------------------------
	states_in_vars(vars,v::rest) => (cr::res1,v::res2)

  rule	states_in_vars(vars,rest) => (res1,res2)
	----------------------------------------
	states_in_vars(vars,v::rest) => (res1,res2)
end

relation reassign_vars:(int list, (* variables *)
		       int list, (* assignments *)
		       int list) (* assignmentsT *)
	=> (int list, int list) (*assignments, assignmentsT*) =

  axiom	reassign_vars([],ass1,ass2) => (ass1,ass2)

  rule	get_assigned(v,ass1,ass2) => vass &
	DAEEXT.get_f(vass) => fassj &
	assign(v,fassj,ass1,ass2) => (ass1,ass2) &
	reassign_vars(vs,ass1,ass2) => (ass1,ass2) 
	---------------------
	reassign_vars(v::vs,ass1,ass2) => (ass1,ass2)
end

relation differentiate_eqns: (DAELow, 
			      IncidenceMatrix, 
			      IncidenceMatrixT,
			      int, (* number of vars*)
			      int, (* number of eqns*)
			      int list) (* equations *)
	=> (DAELow, 
	    IncidenceMatrix, 
	    IncidenceMatrixT,
	    int, (* number of vars*)
	    int, (* number of eqns*) 
	    int list) (* differentiated equations *) =
  axiom	differentiate_eqns(dae,m,mt,nv,nf,[]) => (dae,m,mt,nv,nf,[])

  rule	int_sub(e,1) => e' &
	list_nth(eqns,e') => eqn &
	time_varying_vars(dae) => timevars &
	Derive.differentiate_equation_time(eqn,timevars) => eqn' &
	Debug.fprint("bltdump", "High index problem, differentiated equation: " )&
	print "differentiated equation " &
	equation_str eqn => str & 
	print str & print "\n" &
	Debug.fprint("bltdump", str) &
	Debug.fprint("bltdump", " to ") & 
	equation_str eqn' => str &
	print " to " & print str &  print "\n" &
	Debug.fprint("bltdump",str) & 
	Debug.fprint("bltdump", "\n") &
	list_append(eqns,[eqn']) => eqns' &
	list_length(eqns') => leneqns &
	differentiate_eqns(DAELOW(v,kv,eqns',seqns),m,mt,nv,nf,es) 
	  => (dae,m,mt,nv,nf,reqns)
	  ----------------------------
	differentiate_eqns(dae as DAELOW(v,kv,eqns,seqns),m,mt,nv,nf,e::es) 
	  => (dae,m,mt,nv,nf,leneqns::reqns)

  rule	print "-differentiate_eqns failed\n" 
	----------------------------
	differentiate_eqns(_,_,_,_,_,_) => fail
end

relation time_varying_vars:(DAELow) => Exp.ComponentRef list =

  axiom	time_varying_vars(DAELOW([],_,_,_)) => []

  rule	time_varying_vars(DAELOW(vs,kv,e,se)) => res 
	-----------------------------------------
	time_varying_vars(DAELOW(VAR(cr,STATE,_,_ ,_,_,_,_)::vs,kv,e,se)) 
	  => cr::res

  rule	time_varying_vars(DAELOW(vs,kv,e,se)) => res 
	-----------------------------------------
	time_varying_vars(DAELOW(VAR(cr,DUMMY_DER,_,_, _,_,_,_)::vs,kv,e,se)) 
	  => cr::res

  rule	time_varying_vars(DAELOW(vs,kv,e,se)) => res 
	-----------------------------------------
	time_varying_vars(DAELOW(VAR(cr,VARIABLE,_,_, _,_,_,_)::vs,kv,e,se)) 
	  => cr::res

  rule	time_varying_vars(DAELOW(vs,kv,e,se)) => res 
	-----------------------------------------
	time_varying_vars(DAELOW(VAR(cr,DUMMY_STATE,_,_, _,_,_,_)::vs,kv,e,se)) 
	  => cr::res

  rule	time_varying_vars(DAELOW(vs,kv,e,se)) => res 
	-----------------------------------------
	time_varying_vars(DAELOW(_::vs,kv,e,se)) => res
end
relation add_marked_vars:(DAELow, 
			  IncidenceMatrix, 
			  IncidenceMatrixT,
			  int, (* number of vars*)
			  int, (* number of eqns*)
			  int list) (* marked vars *) 
	=> (DAELow, 
	    IncidenceMatrix, 
	    IncidenceMatrixT,
	    int, (* number of vars*)
	    int) (* number of eqns*) =
  axiom	add_marked_vars(dae,m,mt,nv,nf,[]) => (dae,m,mt,nv,nf)

  rule	int_add(nv,1) => nv' &
	DAEEXT.set_v(v,nv') &
	(* TODO: remove var from dae and m,mt and add der(var) instead*)
	add_marked_vars(dae,m,nt,nv',nf,vs) => (dae,m,mt,nv,nf)
	-------------------------------------------------------
	add_marked_vars(dae,m,nt,nv,nf,v::vs) => (dae,m,mt,nv,nf)
end

relation marked_vars: (int) => int list =

  rule	marked_vars2(0,size) => res
	----------------
	marked_vars(size) => res
end

relation marked_vars2: (int,int) => int list =

  rule	int_eq(size,pos) => true
	------------------------
	marked_vars2(pos,size) => []

  rule	DAEEXT.get_v_mark(pos) => true &
	int_add(pos,1) => v &
	marked_vars2(v,size) => res
	---------------------
	marked_vars2(pos,size) => pos::res

  rule	DAEEXT.get_v_mark(pos) => false &
	int_add(pos,1) => v &
	marked_vars2(v,size) => res
	---------------------
	marked_vars2(pos,size) => res
end

relation marked_eqns: (int) => int list  =

  rule	marked_eqns2(1,size) => res
	----------------
	marked_eqns(size) => res

  rule	print "-marked_eqns failed\n" 
	-----------------
	marked_eqns(_) => fail
end

relation marked_eqns2: (int,int) => int list =

  rule	int_eq(size,pos) => true &
	DAEEXT.get_e_mark(pos) => true 
	------------------------
	marked_eqns2(pos,size) => [pos]

  rule	int_eq(size,pos) => true &
	DAEEXT.get_e_mark(pos) => false
	------------------------
	marked_eqns2(pos,size) => []

  rule	DAEEXT.get_e_mark(pos) => true &
	int_add(pos,1) => v &
	marked_eqns2(v,size) => res
	---------------------
	marked_eqns2(pos,size) => pos::res

  rule	DAEEXT.get_e_mark(pos) => false &
	int_add(pos,1) => v &
	marked_eqns2(v,size) => res
	---------------------
	marked_eqns2(pos,size) => res
end

relation path_found: (IncidenceMatrix, IncidenceMatrixT, int, 
		      int list, int list) 
	=> (int list, int list) =


  rule	int_string  i => is &
	DAEEXT.e_mark(i) (*Sideeffect*) &
	assign_one_in_eqn(m,mt,i,ass1,ass2) => (ass1',ass2')
	------------
	path_found(m,mt,i,ass1,ass2) => (ass1',ass2')

  rule	forall_unmarked_vars_in_eqn(m,mt,i,ass1,ass2) => (ass1',ass2')
	-----------------------------------------------------------
	path_found(m,mt,i,ass1,ass2) => (ass1',ass2')
end

relation assign_one_in_eqn: (IncidenceMatrix, IncidenceMatrixT, int, 
		      int list, int list) => (int list, int list) =
	
  rule  vars_in_eqn(m,i) => vars &
	assign_first_unassigned(i,vars,ass1,ass2) => (ass1',ass2') 
	----------------------------------------------------------
	assign_one_in_eqn(m,mt,i,ass1,ass2) => (ass1',ass2')

(*  rule	print "assign_one_in_eqn failed\n" 
	----------------------------------
	assign_one_in_eqn(_,_,_,_,_) => fail*)
end 

relation states_in_eqn: (Equation, Var list) => int list =

  rule	states_as_algebraic_vars(vars) => vars' &
	incidence_row(vars',eqn) => res
	-----------------
	states_in_eqn(eqn,vars) => res
end

(* Return a sublist of all states, but changed varkind to variable *)
relation states_as_algebraic_vars:( Var list) => Var list =
	
  axiom	states_as_algebraic_vars [] => []

  rule	(* states treated as algebraic variables *)
	states_as_algebraic_vars(vs) => res 
	------------------------------------
	states_as_algebraic_vars(VAR(cr,STATE,a,b,c,d,e,f)::vs) 
	  => (VAR(cr,VARIABLE,a,b,c,d,e,f)::res)

  rule	(* other variables treated as known *)
	states_as_algebraic_vars(vs) => res 
	------------------------------------
	states_as_algebraic_vars(VAR(cr,_,a,b,c,d,e,f)::vs) 
	  => (VAR(cr,CONST,a,b,c,d,e,f)::res)

  rule	states_as_algebraic_vars(vs) => res 
	----------------------
	states_as_algebraic_vars(_::vs) 
	  => res
end

relation vars_in_eqn: (IncidenceMatrix, int) => int list =

  rule	int_sub(n,1) => n' &
	list_nth(m,n') => res
	--------------------
	vars_in_eqn(m,n) => res 

  rule	print "vars_in_eqn failed, indx="  &
	int_string indx => s & print s & print "\n"
	----------------------------
	vars_in_eqn(_,indx) => fail
end

relation eqns_for_var: (IncidenceMatrixT, int) => int list =

  rule	int_sub(n,1) => n' &
	list_nth(mt,n') => res
	--------------------
	eqns_for_var(mt,n) => res 

  rule	print "eqns_for_var failed, indx="  &
	int_string indx => s & print s & print "\n"
	----------------------------
	eqns_for_var(_,indx) => fail
end

relation assign_first_unassigned: (int,int list, int list, int list) 
	  => (int list, int list) =

  rule	get_assigned(v,ass1,ass2) => 0 &
	assign(v,i,ass1,ass2) => (ass1',ass2')
	--------------------------------------
	assign_first_unassigned(i,v::vs,ass1,ass2) => (ass1',ass2')
 
  rule	assign_first_unassigned(i,vs,ass1,ass2) => (ass1',ass2')
	--------------------------------------------------------
	assign_first_unassigned(i,v::vs,ass1,ass2) => (ass1',ass2')
end

relation get_assigned: (int, int list, int list) => int =
	
  rule	int_sub(v,1) => v' &
	list_nth(m,v') => res
	-------------------
	get_assigned(v,m,_) => res
end

relation assign: (int,int, int list, int list) => (int list, int list) =

  rule	(*print "assign " & int_string v => vs &
	int_string e => es & print vs & print " to eqn " &
	print es & print "\n" &*)
	int_sub(v,1) => v' &
	int_sub(e,1) => e' &
	Util.list_replaceat(e,v',ass1) => ass1' &
	Util.list_replaceat(v,e',ass2) => ass2'
	----------------------------------------
	assign(v,e,ass1,ass2) => (ass1',ass2')
end

relation forall_unmarked_vars_in_eqn: (IncidenceMatrix, IncidenceMatrixT, int, 
		      int list, int list) 
	  => (int list, int list)  =
	
  rule	vars_in_eqn(m,i) => vars &
	Util.list_matching(vars,is_not_v_marked) => vars' &
	forall_unmarked_vars_in_eqn_body(m,mt,i,vars',ass1,ass2) 
	  => (ass1',ass2')
	-------------------------
	forall_unmarked_vars_in_eqn (m,mt,i,ass1,ass2) => (ass1',ass2')
end

relation is_not_v_marked: int => () =

  rule	DAEEXT.get_v_mark(i) => false
	------------------
	is_not_v_marked(i)
end

relation forall_unmarked_vars_in_eqn_body: (IncidenceMatrix, IncidenceMatrixT
					    , int
					    , int list (* var list*)
					    , int list (* assignment*) 
					    , int list (* assignment*) )  
	  => (int list, int list)  =

  rule	DAEEXT.v_mark(v) &
	get_assigned(v,ass1,ass2) => assarg &
	path_found(m,mt,assarg,ass1,ass2) => (ass1',ass2') &
	assign(v,i,ass1',ass2') => (ass1'',ass2'')
	----------------------------
	forall_unmarked_vars_in_eqn_body(m,mt,i,vars as (v::vs),ass1,ass2) 
	  => (ass1'',ass2'')
	
  rule	DAEEXT.v_mark(v) &
	forall_unmarked_vars_in_eqn_body(m,mt,i,vs,ass1,ass2) => (ass1',ass2')
	--------------------------------------------------------------------
	forall_unmarked_vars_in_eqn_body(m,mt,i,vars as (v::vs),ass1,ass2)
	  => (ass1',ass2')
end

relation strong_components: (IncidenceMatrix, IncidenceMatrixT,
			     int vector, int vector) => 
	(int list list ) (* list of components *) =

  rule	list_length(m) => n &
	DAEEXT.init_lowlink(n) &
	DAEEXT.init_number(n) &
	strong_connect_main(m,mt,ass1,ass2,n,0,1,[],[]) 
	  => (i,stack,comps) 

	-----------------------------------
	strong_components(m,mt,ass1,ass2) => (comps)

  rule	print "strong_components failed\n" 
	----------------------------------
	strong_components(_,_,_,_) => fail
end

relation strong_connect_main:(IncidenceMatrix,
			 IncidenceMatrixT,
			 int vector, (* Assignment *)
			 int vector, (* Assignment *)
			 int, (* n - number of equations *)
			 int, (* i *)
			 int, (* w *)
			 int list, (* stack *)
		         int list list) (* components *)
	  => (int, (* i *)
	      int list, (* stack *) 
	      int list list) (* components *) =
  rule	int_gt(w,n) => true
	-------------------
	strong_connect_main(m,mt,a1,a2,n,i,w,stack,comp) 
	  => (i,stack,comp)
	
  rule	DAEEXT.get_number(w) => 0 &
	strong_connect(m,mt,a1,a2,i,w,stack,comps)
	  =>(i,stack',comps) &
	int_add(w,1) => w' &
	strong_connect_main(m,mt,a1,a2,n,i,w',stack',comps)
	  => (i,stack'',comps) 
	-----------------------------
	strong_connect_main(m,mt,a1,a2,n,i,w,stack,comps)
	  => (i,stack'',comps)

  rule	DAEEXT.get_number(w) => num &
	int_eq(num,0) => false &
	int_add(w,1) => w' &
	strong_connect_main(m,mt,a1,a2,n,i,w',stack,comps)
	  => (i,stack',comps)
	---------------------------------
	strong_connect_main(m,mt,a1,a2,n,i,w,stack,comps)
	  =>(i,stack',comps)
end

relation strong_connect: (IncidenceMatrix,
			  IncidenceMatrixT,
			  int vector,
			  int vector,
			  int, (* i *)
			  int, (* v *)
			  int list, (* stack *)
			  int list list) (* components *)
	  => (int, (* i *)
	      int list, (* stack *)
	      int list list ) (* int list list*) =

  rule	int_add(i,1) => i' &
(*	set_number(number,v,i') => number' &
	set_lowlink(lowlink,v,i') => lowlink' &*)
	DAEEXT.set_number(v,i') &
	DAEEXT.set_lowlink(v,i') &
	let stack' = v::stack & 
	reachable_nodes(v,m,mt,a1,a2) => eqns &
	iterate_reachable_nodes(eqns,m,mt,a1,a2,i',v,stack',comps)
	  => (i',stack'',comps') &
	check_root(m,mt,a1,a2,i',v,stack'') 
	  => (i',stack''',comp) &
	cons_if_nonempty(comp,comps') => comps''
	---------------------------------------------
	strong_connect(m,mt,a1,a2,i,v,stack,comps) 
	  => (i',stack''',comps'')
end

relation cons_if_nonempty: (int list, int list list) => int list list =
  axiom	cons_if_nonempty ([],lst) => lst 

  axiom	cons_if_nonempty (e,lst) => e::lst
end 

relation reachable_nodes: (int,
			   IncidenceMatrix,
			   IncidenceMatrixT,
			   int vector,
			   int vector) => int list =
	
  rule	int_sub(eqn,1) => eqn' &
	vector_nth(a2,eqn') => var &
	int_sub(var,1) => var' &
	list_nth(mt,var') => reachable &
	Util.list_position(eqn,reachable) => pos &
	list_delete(reachable,pos) => reachable'
	----------------------------------------
	reachable_nodes(eqn,m,mt,a1,a2) => reachable'
end			  

relation iterate_reachable_nodes: (int list, 
				   IncidenceMatrix,
				   IncidenceMatrixT,
				   int vector,
				   int vector,
				   int, (* i *)
				   int, (* v *)
				   int list, (* stack *)
				   int list list) (* components *)
	  => (int, (* i *)
	      int list, (* stack *) 
	      int list list) (* components *) =
	
  rule	DAEEXT.get_number(w) => 0 &
	strong_connect(m,mt,a1,a2,i,w,stack,comps) 
	  => (i,stack,comps') &
	DAEEXT.get_lowlink(v) => lv &
	DAEEXT.get_lowlink(w) => lw &
	int_min(lv,lw) => minv &
	DAEEXT.set_lowlink(v,minv) &
	iterate_reachable_nodes(ws,m,mt,a1,a2,i,v,stack,comps') 
	  => (i,stack,comps'')
	  ----------------------------
	iterate_reachable_nodes(w::ws,m,mt,a1,a2,i,v,stack,comps) 
	  => (i, stack,comps'')
	  
  rule	DAEEXT.get_number(w) => nw &
	DAEEXT.get_number(v) => nv &
	int_lt(nw,nv) => true &
	list_member(w,stack) => true &
	DAEEXT.get_lowlink(w) => lowlinkw &
	int_min(nw,lowlinkw) => minv &
	DAEEXT.set_lowlink(v,minv) &
	iterate_reachable_nodes(ws,m,mt,a1,a2,i,v,stack,comps) 
	  => (i,stack,comps')
	  -----------------------
	iterate_reachable_nodes(w::ws,m,mt,a1,a2,i,v,stack,comps)
	  => (i,stack,comps')
	  
  rule	iterate_reachable_nodes(ws,m,mt,a1,a2,i,v,stack,comps) 
	  => (i,stack,comps')
	  -----------------------------
	iterate_reachable_nodes(w::ws,m,mt,a1,a2,i,v,stack,comps)
	  => (i,stack,comps')
	  
  axiom iterate_reachable_nodes([],m,mt,a1,a2,i,v,stack,comps)
	  => (i,stack,comps)
end

relation dump_list: (int list, string) => () =

  rule	Util.list_map(l,int_string) => s &
	Util.string_delimit_list(s,", ") => sl &
	print str & print sl & print "\n" 
	-------------------
	dump_list(l,str) => ()
end
	    
relation check_root: (IncidenceMatrix,
		      IncidenceMatrixT,
		      int vector,
		      int vector,
		      int, (* i *)
		      int, (* v *)
		      int list) (* stack *)
		     
	  => (int, (* i *)
	      int list, (* stack *) 
	      int list) (* comps *) =

  rule	DAEEXT.get_lowlink(v) => lv &
	DAEEXT.get_number(v) => nv &
	int_eq(lv,nv) => true &
	check_stack(m,mt,a1,a2,i,v,stack,[])
	=> (i,stack',comps) 
	------------------------------------
	check_root(m,mt,a1,a2,i,v,stack)
	  => (i, stack',comps)

  axiom	check_root(m,mt,a1,a2,i,v,stack) 
	  => (i,stack,[])	
end

relation check_stack: (IncidenceMatrix,
		       IncidenceMatrixT,
		       int vector,
		       int vector,
		       int, (* i *)
		       int, (* v *)
		       int list, (* stack *)
		       int list) (* component list*)
		     
	  => (int, (* i *) 
	      int list, (* stack *) 
	      int list) (* comps *) =

  rule	DAEEXT.get_number(top) => topn &
	DAEEXT.get_number(v) => vn &
	int_ge(topn,vn) => true &
	check_stack(m,mt,a1,a2,i,v,rest,comp) 
	  => (i,stack',comp') 
	  -----------------------------
	check_stack(m,mt,a1,a2,i,v,top::rest,comp)
	  => (i,stack',top::comp')
	  
  axiom	check_stack(m,mt,a1,a2,i,v,stack,comp) 
	  => (i,stack,comp)
end

relation dump_components: (int list list) => () =

  rule	print "Blocks\n" &
	print "=======\n" &
	Util.list_map_0(l,dump_components2)
	---------------------
	dump_components (l) => ()
end


relation dump_components2: (int list) => () =
	
  rule	print "{" & 
	Util.list_map(l,int_string) => ls &
	Util.string_delimit_list(ls,", ") =>  s &
	print s & print "}\n" 
	---------------------
	dump_components2(l) 
end
(* Translates the dae so variables are indexed into different arrays:
 xd for derivatives
 x for states
 dummy_der for dummy derivatives
 dummy for dummy states
 y for algebraic variables 
 p for parameters

 The equations are updated with the new variable names.
*)
relation translate_dae: DAELow => DAELow =

  rule	calculate_indexes(vars) => vars' &
	calculate_indexes(kv) => kv' &
	list_append(vars',kv') => totvars &
	variable_replacements(totvars,eqns) => (s,t) &
	Util.list_map(s,Exp.print_exp_str) => sl &
	Util.string_delimit_list(sl,", ") => sstr &
	Util.list_map(t,Exp.print_exp_str) => tl &
	Util.string_delimit_list(tl,", ") => tstr &
	print "replace " & print sstr & print "with " & print tstr & print "\n" &
	replace_variables(eqns,s,t) => eqns' &
	transform_variables(vars') => vars'' 
	------------------------------------
	translate_dae(DAELOW(vars,kv,eqns,seqns)) => DAELOW(vars'',kv,eqns',seqns)
end

relation calculate_sizes: DAELow => (int, (* nx *)
				     int, (* ny *)
				     int) (* np *) =
  rule	calculate_param_sizes(knvars) => np &
	calculate_var_sizes(vars,0,0) => (nx,ny)
	-----------------------------------
	calculate_sizes(DAELOW(vars,knvars,_,_)) => (nx,ny,np)
end

relation calculate_param_sizes:( Var list) => (int) =

  axiom	calculate_param_sizes([]) => 0

  rule	calculate_param_sizes(vs) => s1 &
	int_add(s1,1) => s2 
	------------------
	calculate_param_sizes(VAR(_,PARAM,_,_,_,_,_,_)::vs) 
	  => (s2)

  rule	calculate_param_sizes(vs) => s1 
	------------------
	calculate_param_sizes(VAR(_,_,_,_,_,_,_,_)::vs) 
	  => (s1)
end
	
relation calculate_var_sizes:( Var list, int, int) => (int, int) =

  axiom	calculate_var_sizes([],nx,ny) => (nx,ny)

  rule	int_add(ny,1) => ny' &
	calculate_var_sizes(vs,nx,ny') => (nx'',ny'')
	----------------------------
	calculate_var_sizes(VAR(_,VARIABLE,_,_,_,_,_,_)::vs,nx,ny) 
	  => (nx'',ny'')

  rule	int_add(nx,1) => nx' &
	calculate_var_sizes(vs,nx',ny) => (nx'',ny'')
	----------------------------
	calculate_var_sizes(VAR(_,STATE,_,_,_,_,_,_)::vs,nx,ny) 
	  => (nx'',ny'')

  rule	int_add(ny,1) => ny' &
	calculate_var_sizes(vs,nx,ny') => (nx'',ny'')
	----------------------------
	calculate_var_sizes(VAR(_,DUMMY_STATE,_,_,_,_,_,_)::vs,nx,ny) 
	  => (nx'',ny'')

  rule	int_add(ny,1) => ny' &
	calculate_var_sizes(vs,nx,ny') => (nx'',ny'')
	----------------------------
	calculate_var_sizes(VAR(_,DUMMY_DER,_,_,_,_,_,_)::vs,nx,ny) 
	  => (nx'',ny'')

  rule	print "-calculate_var_sizes failed\n" 
	----------------------------
	calculate_var_sizes(_,_,_) 
	  => fail
end


(* Transforms the equations, given two lists with source and target expressions*)
relation replace_variables: (Equation list, Exp.Exp list, Exp.Exp list) => (Equation list) =

  axiom	replace_variables ([],_,_) => []

  rule	Exp.replace_exp_list(e1,s,t) => (e1',_) &
	Exp.replace_exp_list(e2,s,t) => (e2',_) &
	replace_variables(es,s,t) => es'
	---------------------------------------
	replace_variables(EQUATION(e1,e2)::es,s,t) => EQUATION(e1',e2')::es'

  rule	print "-replace_variables failed\n" 
	------------------
	replace_variables(_,_,_) => fail
end

(* Returns a two list of replacement expressions for variable transformations.
 For instance, replacing state 's' with 'x[3]' and 'der(s)' with 'xd[3]',
 NOTE: The derivative expressions must be first, so they are replaced first
 i.e 'der(s)' is replaced before 's' is replaced which gives a wrong 
 variable like 'der(x[5)'
 *)
relation variable_replacements: (Var list, Equation list) => (Exp.Exp list, Exp.Exp list) =
	 
  rule	states_eqns(eqns) => states  &
	derivative_replacements(states,vars) => (s1,t1) &
	alg_variable_replacements(vars) => (s2,t2) &
	list_append(s1,s2) => s &
	list_append(t1,t2) => t
	---------------------
	variable_replacements(vars,eqns) =>  (s,t)

  rule	print "-variable_replacements failed\n" 
	---------------------------------------
	variable_replacements(vars,eqns) =>  fail
end

(* Build replacement "rules" for the variables, eg. states, algebraic variables, parameters, etc. *)
relation alg_variable_replacements: (Var list) => (Exp.Exp list, Exp.Exp list) =

  axiom	alg_variable_replacements([]) => ([],[])

  rule	alg_variable_replacements(vs) => (s1,t1) &
	int_string(indx) => indxs &
	Util.string_append_list(["y[",indxs,"]"]) => newid
	--------------------
	alg_variable_replacements(VAR(cr,VARIABLE,_,_,_,_,_,indx)::vs)
	  => (Exp.CREF(cr,Exp.REAL)::s1,Exp.CREF(Exp.CREF_IDENT(newid,[]),Exp.REAL)::t1)

  rule	alg_variable_replacements(vs) => (s1,t1) &
	int_string(indx) => indxs &
	Util.string_append_list(["x[",indxs,"]"]) => newid
	--------------------
	alg_variable_replacements(VAR(cr,STATE,_,_,_,_,_,indx)::vs)
	  => (Exp.CREF(cr,Exp.REAL)::s1,Exp.CREF(Exp.CREF_IDENT(newid,[]),Exp.REAL)::t1)

  rule	alg_variable_replacements(vs) => (s1,t1) &
	int_string(indx) => indxs &
	Util.string_append_list(["y[",indxs,"]"]) => newid
	--------------------
	alg_variable_replacements(VAR(cr,DUMMY_DER,_,_,_,_,_,indx)::vs)
	  => (Exp.CREF(cr,Exp.REAL)::s1,Exp.CREF(Exp.CREF_IDENT(newid,[]),Exp.REAL)::t1)

  rule	alg_variable_replacements(vs) => (s1,t1) &
	int_string(indx) => indxs &
	Util.string_append_list(["y[",indxs,"]"]) => newid
	--------------------
	alg_variable_replacements(VAR(cr,DUMMY_STATE,_,_,_,_,_,indx)::vs)
	  => (Exp.CREF(cr,Exp.REAL)::s1,Exp.CREF(Exp.CREF_IDENT(newid,[]),Exp.REAL)::t1)

  rule	alg_variable_replacements(vs) => (s1,t1) &
	int_string(indx) => indxs &
	Util.string_append_list(["y[",indxs,"]"]) => newid
	--------------------
	alg_variable_replacements(VAR(cr,DISCRETE,_,_,_,_,_,indx)::vs)
	  => (Exp.CREF(cr,Exp.REAL)::s1,Exp.CREF(Exp.CREF_IDENT(newid,[]),Exp.REAL)::t1)

  rule	alg_variable_replacements(vs) => (s1,t1) &
	int_string(indx) => indxs &
	Util.string_append_list(["p[",indxs,"]"]) => newid
	--------------------
	alg_variable_replacements(VAR(cr,PARAM,_,_,_,_,_,indx)::vs)
	  => (Exp.CREF(cr,Exp.REAL)::s1,Exp.CREF(Exp.CREF_IDENT(newid,[]),Exp.REAL)::t1)

  rule	alg_variable_replacements(vs) => (s1,t1) &
	int_string(indx) => indxs &
	Util.string_append_list(["p[",indxs,"]"]) => newid
	--------------------
	alg_variable_replacements(VAR(cr,STRUCTPARAM,_,_,_,_,_,indx)::vs)
	  => (Exp.CREF(cr,Exp.REAL)::s1,Exp.CREF(Exp.CREF_IDENT(newid,[]),Exp.REAL)::t1)

  rule	alg_variable_replacements(vs) => (s1,t1) &
	int_string(indx) => indxs &
	Util.string_append_list(["p[",indxs,"]"]) => newid
	--------------------
	alg_variable_replacements(VAR(cr,CONST,_,_,_,_,_,indx)::vs)
	  => (Exp.CREF(cr,Exp.REAL)::s1,Exp.CREF(Exp.CREF_IDENT(newid,[]),Exp.REAL)::t1)

  rule	print "-alg_variable_replacements failed\n" 
	-------------------------------------------
	alg_variable_replacements(_) => fail
end


relation states_eqns: Equation list => Exp.ComponentRef list =
	
axiom	states_eqns [] => []

  rule	states_eqns(es) => s &
	states_exp(e1) => s1 &
	states_exp(e2) => s2 &
	Util.list_list_union_p([s,s1,s2],Exp.cref_equal) => res
	-----------------
	states_eqns(EQUATION(e1,e2)::es) => res
end

relation derivative_replacements: (Exp.ComponentRef list, Var list) 
	  => (Exp.Exp list, Exp.Exp list) =

  axiom	derivative_replacements([],_) => ([],[])
	
  rule	derivative_replacements(ss,vars)  => (s1,t1) &
	get_index(s,vars) => indx &
	int_string(indx) => indxs &
	Util.string_append_list(["xd[",indxs,"]"]) => newid
	------------------------------------------
	derivative_replacements(s::ss,vars) 
	  => (Exp.CALL(Absyn.IDENT("der"),
		       [Exp.CREF(s,Exp.REAL)],
		       false,
		       false
		       )::s1,
	      Exp.CREF(Exp.CREF_IDENT(newid,[]),Exp.REAL)::t1)

  rule	print "-derivative_replacements failed\n" 
	----------------------
	derivative_replacements(_,_) => fail
end

relation get_index: (Exp.ComponentRef, Var list) => int =

  rule	Exp.cref_equal(cr1,cr2) => true
	-------------------------------
	get_index(cr1,VAR(cr2,_,_,_,_,_,_,indx)::_) => indx

  rule	get_index(cr1,vs) => indx
	-------------------------
	get_index(cr1,_::vs) => indx
end

relation calculate_indexes: (Var list) => Var list =
	
  rule	calculate_indexes2(vars,0,0,0,0,0) => vars'
	----------------------------------------
	calculate_indexes(vars) => vars'

  rule	print "-calculate_indexes failed\n" 
	------------------------
	calculate_indexes(_) => fail
end

relation transform_variables: (Var list ) => (Var list) =

  axiom transform_variables([]) => []

  rule	transform_variables(vs) => vs' &
	transform_variable(i,kind) => cr'
	-------------------
	transform_variables(VAR(cr,kind,a,b, c,d,e,i)::vs)
	=> (VAR(cr',kind,a,b,c,d,e,i)::vs')
end

relation transform_variable: (int, VarKind) => Exp.ComponentRef =

  rule	int_string(i) => is &
	Util.string_append_list(["y[",is,"]"]) => id
	-------------------
 	transform_variable(i, VARIABLE) => Exp.CREF_IDENT(id,[])

  rule	int_string(i) => is &
	Util.string_append_list(["x[",is,"]"]) => id
	-------------------
 	transform_variable(i, STATE) => Exp.CREF_IDENT(id,[])

  rule	int_string(i) => is &
	Util.string_append_list(["y[",is,"]"]) => id
	-------------------
 	transform_variable(i, DUMMY_DER) => Exp.CREF_IDENT(id,[])

  rule	int_string(i) => is &
	Util.string_append_list(["y[",is,"]"]) => id
	-------------------
 	transform_variable(i, DUMMY_STATE) => Exp.CREF_IDENT(id,[])

  rule	int_string(i) => is &
	Util.string_append_list(["y[",is,"]"]) => id
	-------------------
 	transform_variable(i, DISCRETE) => Exp.CREF_IDENT(id,[])

  rule	int_string(i) => is &
	Util.string_append_list(["p[",is,"]"]) => id
	-------------------
 	transform_variable(i, PARAM) => Exp.CREF_IDENT(id,[])

  rule	int_string(i) => is &
	Util.string_append_list(["p[",is,"]"]) => id
	-------------------
 	transform_variable(i, CONST) => Exp.CREF_IDENT(id,[])
end

relation calculate_indexes2: (Var list,int,int,int,int,int) => Var list =

  axiom	calculate_indexes2([],_,_,_,_,_) => []
	
  rule	int_add(y,1) => y' &
	calculate_indexes2(vs,x,xd,y',p,dummy) => vars' 
	----------------------------------------
	calculate_indexes2(VAR(cr,VARIABLE,d,tp,b,dim,start,_)::vs,x,xd,y,p,dummy) 
	  => VAR(cr,VARIABLE,d,tp,b,dim,start,y)::vars'

  rule	int_add(x,1) => x' &
	calculate_indexes2(vs,x',xd,y,p,dummy) => vars' 
	----------------------------------------
	calculate_indexes2(VAR(cr,STATE,d,tp,b,dim,start,_)::vs,x,xd,y,p,dummy) 
	  => VAR(cr,STATE,d,tp,b,dim,start,x)::vars'

 rule	(* Dummy derivatives become algebraic variables *)
	int_add(y,1) => y' &
	calculate_indexes2(vs,x,xd,y',p,dummy) => vars' 
	----------------------------------------
	calculate_indexes2(VAR(cr,DUMMY_DER,d,tp,b,dim,start,_)::vs,x,xd,y,p,dummy) 
	  => VAR(cr,DUMMY_DER,d,tp,b,dim,start,y)::vars'

  rule	(* Dummy state become algebraic variables *)
	int_add(y,1) => y' &
	calculate_indexes2(vs,x,xd,y',p,dummy) => vars' 
	----------------------------------------
	calculate_indexes2(VAR(cr,DUMMY_STATE,d,tp,b,dim,start,_)::vs,x,xd,y,p,dummy) 
	  => VAR(cr,DUMMY_STATE,d,tp,b,dim,start,y)::vars'

  rule	int_add(y,1) => y' &
	calculate_indexes2(vs,x,xd,y',p,dummy) => vars' 
	----------------------------------------
	calculate_indexes2(VAR(cr,DISCRETE,d,tp,b,dim,start,_)::vs,x,xd,y,p,dummy) 
	  => VAR(cr,DISCRETE,d,tp,b,dim,start,y)::vars'

  rule	int_add(p,1) => p' &
	calculate_indexes2(vs,x,xd,y,p',dummy) => vars' 
	----------------------------------------
	calculate_indexes2(VAR(cr,PARAM,d,tp,b,dim,start,_)::vs,x,xd,y,p,dummy) 
	  => VAR(cr,PARAM,d,tp,b,dim,start,p)::vars'

  rule	int_add(p,1) => p' &
	calculate_indexes2(vs,x,xd,y,p',dummy) => vars' 
	----------------------------------------
	calculate_indexes2(VAR(cr,CONST,d,tp,b,dim,start,_)::vs,x,xd,y,p,dummy) 
	  => VAR(cr,CONST,d,tp,b,dim,start,p)::vars'
end

relation print_equations: (int list, DAELow) => () =

  axiom	print_equations ([],_) => ()

  rule	print_equations(rest,dae) => () &
	print_equation_no(n,dae) => ()
	----------------
	print_equations(n::rest,dae) => ()
end

relation print_equation_no: (int, DAELow) => () =

  rule	int_sub(eqno,1) => eqno' &
	list_nth(eqns,eqno') => eq &
	print_equation eq => ()
	----------------
	print_equation_no(eqno,DAELOW(_,_,eqns,_)) => ()
end

relation print_equation: Equation =>  () =

  rule	Exp.print_exp_str e1 => s1 &
	Exp.print_exp_str e2 => s2 &
	Util.string_append_list([s1, " = ", s2, "\n" ]) => res &
	print res
	-----------------
	print_equation(EQUATION(e1,e2))
end