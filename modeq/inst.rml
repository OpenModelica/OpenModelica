(* inst.rml *)

module Inst:

  with "exp.rml"
  with "absyn.rml"
  with "explode.rml"
  with "dae.rml"

  relation instantiate: Explode.Program => DAE.DAEform

end

with "prefix.rml"
with "env.rml"
with "mod.rml"
with "builtin.rml"
with "dump.rml"
with "types.rml"
with "connect.rml"

(* These make the code a little more readable *)
type Prefix = Prefix.Prefix
type Mod = Mod.Mod
type Ident = Exp.Ident
type Env = Env.Env


(**********************************************************************)

relation assert : (bool, bool, string) => () =

  rule  x = y
	-----
	assert(x,y,_)

  rule  not x = y &
	print "ERROR: " & print m & print "\n"
	--------------------------------------
	assert(x,y,m)

end

relation optional_cons : ('a option, 'a list) => 'a list =

  axiom optional_cons(NONE, l) => l

  axiom optional_cons(SOME(x), l) => x::l

end

(**********************************************************************)

relation inst_list : (Env, Mod, Prefix, Connect.Sets,
		      (Env, Mod, Prefix, Connect.Sets, 'a) => ('b list, Env,
							       Connect.Sets),
		      'a list)
	  => ('b list, Env, Connect.Sets) =

  axiom inst_list(env,mod,pre,csets,r,[]) => ([],env,csets)

  rule	r(env,mod,pre,csets,e) => (l,env',csets') &
	inst_list(env',mod,pre,csets',r,es) => (l',env'',csets'') &
	list_append(l,l') => l''
	--------------------------------------------
	inst_list(env,mod,pre,csets,r,e::es) => (l'',env'',csets'')

end

	(* FIXME flow variables. This needs to be totally reworked *)

relation connect_vars : (Connect.Sets, Exp.ComponentRef, Exp.ComponentRef,
			 Types.Var list) => Connect.Sets =

  axiom	connect_vars(sets,_,_,[]) => sets

  rule	Exp.extend_cref(c1, n, []) => c1' &
	Exp.extend_cref(c2, n, []) => c2' &
	print "  connect_vars: " &
	Dump.dump_component_ref c1' & print " <-> " &
	Dump.dump_component_ref c2' &
	print "\n" &
	connect_components(sets,[], c1', c2', ty, flow) => sets' &
	connect_vars(sets',c1,c2,xs) => sets''
	--------------------------------------
	connect_vars(sets,c1,c2,(n,Types.ATTR(_,flow,_,_),ty)::xs) => sets''

end

relation connect_components: (Connect.Sets,
			      Prefix,
			      Explode.ComponentRef,
			      Explode.ComponentRef,
			      Types.Type, bool) => Connect.Sets =
	
  rule	Prefix.prefix_cref(pre, c1) => c1' &
	Prefix.prefix_cref(pre, c2) => c2' &
	print "  connect_components flow: " &
	Dump.dump_component_ref c1' & print " <-> " &
	Dump.dump_component_ref c2' &
	print "\n" &
	Connect.add_flow(sets, c1, Connect.IN, c2, Connect.IN) => sets'
	-----------------------------------------
	connect_components(sets, pre, c1, c2, Types.BUILTIN(_), true) => sets'
	
  rule	Prefix.prefix_cref(pre, c1) => c1' &
	Prefix.prefix_cref(pre, c2) => c2' &
	print "  connect_components non-flow: " &
	Dump.dump_component_ref c1' & print " <-> " &
	Dump.dump_component_ref c2' &
	print "\n" &
	Connect.add_equ(sets, c1, c2) => sets'
	-----------------------------------------
	connect_components(sets, pre, c1, c2, Types.BUILTIN(_), false) => sets'
	
  rule	Prefix.prefix_cref(pre, c1) => c1' &
	Prefix.prefix_cref(pre, c2) => c2' &
	print "  connect_components 2: " &
	Dump.dump_component_ref c1' & print " <-> " &
	Dump.dump_component_ref c2' &
	print "\n" &
	connect_vars(sets,c1',c2', l) => sets'
	----------------------------------------
	connect_components(sets,pre,c1,c2,Types.COMPLEX(l),_) => sets'
	
end

relation check_flow : (bool, bool) => () =

  axiom	check_flow(false,false)
  axiom	check_flow(true,true)

end

relation inst_connect: (Connect.Sets, Env, Prefix,
			Explode.ComponentRef,Explode.ComponentRef)
	  => Connect.Sets =

  rule	print " trying to connect " &
	Dump.dump_component_ref(o1) & print " with " &
	Dump.dump_component_ref(o2) & print "\n" &
	Env.lookup_var(env,o1) => (Types.ATTR(_,flow1,_,dir1),ty1) &
	Env.lookup_var(env,o2) => (Types.ATTR(_,flow2,_,dir2),ty2) &
	Types.same_type(ty1, ty2) &
	check_flow(flow1, flow2) &
	connect_components(sets, pre, o1, o2, ty1, flow1) => sets'
	---------------------------------
	inst_connect(sets, env,pre,o1,o2) => sets'

  rule	print "# could not connect\n"
	---------------------------------
	inst_connect(sets,_,_,_,_) => fail

end

relation inst_equation: (Env,Mod,Prefix,Connect.Sets,Explode.Equation)
	  => (DAE.DAEcomp list, Env, Connect.Sets) =

  (* connect
   *
   * Handle connct statements
   *)

  rule	inst_connect(csets,env,pre,c1,c2) => csets'
	------------------------
	inst_equation(env,mods,pre, csets, Absyn.EQ_CONNECT(c1,c2))
	 => ([],env,csets')

  (* assert
   *
   * FIXME: Handle assertions
   *)

  rule	Prefix.prefix_exp(e,pre) => e' &
	print " ASSERTION(" & Dump.dump_exp(e') & print "): " &
	print d & print "\n"
	------------------------
	inst_equation(env,mods,pre,csets,
		      Absyn.EQ_EXPR(Exp.CALL(Exp.IDENT("assert"),
					     [e,Exp.STRING(d)])))
	  => ([],env,csets)

  rule	print "No expression equations yet\n"
	-------------------------------------
	inst_equation(env,mods,pre,csets,Absyn.EQ_EXPR(_)) => fail

  rule	Env.lookup_var_local(env,n) => Env.FRAMEVAR(_,_,_,SOME(_)) &
	print " shadowed equation for " & print n & print "\n"
	------------------------------------------------------
	inst_equation(env,mods,pre,csets,
		      Absyn.EQ_EQUALS(Exp.CREF([(n,[])]), e2))
	  => ([],env,csets)

  rule	print " equation\n" &
	Prefix.prefix_exp(e1,pre) => e1' &
	Prefix.prefix_exp(e2,pre) => e2'
	------------------------
	inst_equation(env,mods,pre,csets,Absyn.EQ_EQUALS(e1,e2))
	  => ([DAE.EQUATION(Exp.RELATION(e1',Exp.EQUAL,e2'))],env,csets)

end

relation inst_mod_assignment : (Prefix, Ident, Mod) => DAE.DAEcomp list =

  rule  Prefix.prefix_cref(pre, [(v,[])]) => v' &
	
	print "  assignment modification: " &
 	Dump.dump_component_ref v' &
 	Mod.dump mod &
	print "\n" &
	
	Prefix.prefix_exp(e, pre) => e'
	-------------------------------
	inst_mod_assignment(pre,v,mod as Mod.MOD(_,_,SOME(e)))
	  => [DAE.EQUATION(Exp.RELATION(Exp.CREF(v'), Exp.EQUAL, e'))]
	  
  axiom inst_mod_assignment(_,_,_) => []
  
end

relation check_prot : (bool, Mod, Exp.ComponentRef) => () =

  rule  print "  public variable " & Dump.dump_component_ref cref & print "\n"
	--------------------------------------------------
	check_prot(false,_,cref)
	
  axiom check_prot(_,Mod.NOMOD,_)

  rule  print "ERROR: Trying to modify protected element " &
	Dump.dump_component_ref cref & print "\n"
	-----------------------------------------
	check_prot(true,_,cref) => fail

end

relation inst_element: (Env, Mod, Prefix, Connect.Sets, Explode.Element)
	  => (DAE.DAEcomp list, Env, Connect.Sets, Types.Var list) =

	(* extends
	 *
	 * Handle "extends" elements by instantiating the class definition
	 * of the extended class.
	 *)
	 
  rule	Env.lookup_class(env,cn)
	  => (c as Explode.CLASS(_,partial,_,_),classmod) &
	
	Exp.path_string(cn) => cns &
	print "  extending with class " & print cns & print "\n" &
	
	(* Can't extend partial classes *)
	assert(partial,false,"Can't extend partial class") &
	
	(* Build the combind set of modifications *)
	(* classmod is the modifications stored with the class *)
	(* mods     is the modifications passed to the relation *)
	(* m        is the modification stored in the element *)
	(* FIXME: verify order *)
	print "  modification 1: " & print cns & Mod.dump classmod & print "\n" &
	print "  modification 2: " & print cns & Mod.dump mods & print "\n" &
	print "  modification 3: " & print cns & Mod.dump m & print "\n" &
	Mod.merge(classmod,mods) => mods' &
	Mod.merge(mods',m) => mods'' &
	
	(* Can't use inst_class, as that creates a new frame *)
	inst_class_in(env,mods'',pre,csets,c,prot) => (dae,env',csets',tys)
	----------------------------------
	inst_element(env,mods,pre,csets,
	Explode.ELEMENT(final,prot,_,Explode.EXTENDS(cn,m)))
	  => (dae,env',csets',tys)
	
  	(* components
	 *
	 * Rules for instantiating components.
	 *)
	 
	 (* If a variable is declared multiple times, the first is used *)
  rule	Env.lookup_var_local(env,n) => _ &
	print " ignoring shadowed variable " & print n & print "\n"
	--------------------------
	inst_element(env,mods,pre,csets,
		     Explode.ELEMENT(final,prot,n,
				     Explode.COMPONENT(_,_,_,_)))
	  => ([],env,csets,[])
	
  	(* Illegal redeclarations *)
  rule	Env.lookup_var_local(env,n) => _ &
	print "Trying to redeclare the class " & print n &
	print " as a variable\n"
	------------------------
	inst_element(env,mods,pre,csets,
		     Explode.ELEMENT(final,prot,n, Explode.CLASSDEF(_,_)))
	  => fail

  rule	Env.lookup_class(env,Exp.IDENT(n)) => (v,_) &
	print "Trying to redeclare the variable " & print n &
	print " as a class\n"
	---------------------
	inst_element(env,mods,pre,csets,
		     Explode.ELEMENT(final,prot,n,
				     Explode.COMPONENT(_,_,_,_)))
	  => fail

  (* A new class definition
   *
   * Put it in the current frame in the environment
   *)
   
  rule	(* Explode.convert(c) => c' & *)
	Mod.lookup_modification(mods,n) => classmod &
	print "  adding " & print n & Mod.dump classmod & print "\n" &
	(* Mod.simple_maybe_modify_class(env,mm,pre,c') => (c'',dae) & *)
	Env.extend_frame_c(env,c,classmod) => env'
	----------------------------------------
	inst_element(env,mods,pre,csets,
		     Explode.ELEMENT(final,prot,n,
				     Explode.CLASSDEF(_,c)))
	  => ([],env',csets,[])

  (* A variable
   *
   * Lookup the class name, apply specializations and add the variable
   * to the current frame in the environment. Then instantiate the class
   * with an extended prefix.
   *)

  rule	Prefix.prefix_cref(pre,[(n,[])]) => vn &

	print " variable " & Dump.dump_component_ref vn & print "\n" &

	(* Find the set of modifications *)
	(* FIXME: merge order *)
	Env.lookup_class(env,t) => (cl,classmod) &
	print "  modification 1: " & Mod.dump classmod & print "\n" &

	Mod.lookup_modification(mods,n) => mm &
	print "  modification 2: " & Mod.dump mm & print "\n" &
	print "  modification 3: " & Mod.dump m & print "\n" &

	(* classmod is the modifications stored with the class *)
	(* mm       is the modifications passed to the relation *)
	(* m        is the modification stored in the element *)

	(* Check for modifications to a protected element *)
	check_prot(prot, mm, vn) &

	Mod.merge(classmod,mm) => mod &
	Mod.merge(mod,m) => mod' &
	print "  modification: " & Mod.dump mod' & print "\n" &

	Prefix.prefix_add(n,[],pre) => pre' &

	inst_class(env,mod',pre',csets,cl) => (dae1,csets',ty) &

	Mod.mod_assignment mod' => ass &
	Env.extend_frame_v(env,Env.FRAMEVAR(n,attr,ty,ass)) => env' &
	print " extended frame with variable " & Dump.dump_component_ref vn &
 	print "\n" &

	(* If the modification assigns a value to this variable, add an
	   equation *)
	inst_mod_assignment(pre,n,mod') => dae2 &

	list_append(dae1, dae2) => dae
	--------------------------
	inst_element(env,mods,pre,csets,
		     Explode.ELEMENT(final,prot,_,
				     Explode.COMPONENT(n,attr,t,m)))
         => (dae,env',csets',[(n,attr,ty)])

  (*
   * If the class lookup in the previous rule fails, this rule catches
   * the error and prints an error message about the unknown class.
   *)

  rule	not Env.lookup_class(env,t) => (cl,_) &
	Exp.path_string(t) => s &
	print "# unknown class '" & print s & print "' while instantiating " &
	Prefix.prefix_path(Exp.IDENT(n),pre) => n' &
	Exp.path_string(n') => ns &
	print ns & print "\n"
	--------------------------
	inst_element(env,_,pre,csets,
		     Explode.ELEMENT(final,prot,n,
				     Explode.COMPONENT(_,_,t,_)))
         => ([],env,csets,[])

end

and inst_element_list : (Env, Mod, Prefix, Connect.Sets, Explode.Element list)
	  => (DAE.DAEcomp list, Env, Connect.Sets, Types.Var list) =

  axiom inst_element_list(env,_,_,csets,[]) => ([], env, csets, [])

  rule  inst_element(env,mod,pre,csets,el) => (dae1,env',csets',tys1) &
	inst_element_list(env',mod,pre,csets',els)
	  => (dae2,env'',csets'',tys2) &
	  list_append(tys1, tys2) => tys &
	list_append(dae1, dae2) => dae
	------------------------------
	inst_element_list(env,mod,pre,csets,el::els)
	  => (dae, env'', csets'', tys)

end

and inst_algorithm : (Env, Mod, Prefix, Connect.Sets, Explode.Algorithm)
	  => (DAE.DAEcomp list, Env, Connect.Sets) =

  rule  print "# no algorithms yet\n"
	-----------------------------
	inst_algorithm(env,_,_,csets,_) => ([],env,csets)

end

and inst_classdef: (Env, Mod, Prefix, Connect.Sets, Explode.ClassDef, bool)
	  => (DAE.DAEcomp list, Env, Connect.Sets, Types.Var list) =

	(* FIXME: propagate prot *)
  rule	inst_element_list(env,mods,pre,csets,els) => (dae1,env', csets', tys) &
	inst_list(env',mods,pre,csets',inst_equation,eqs)
	  => (dae2,env'',csets'') &
	inst_list(env'',mods,pre,csets'',inst_algorithm,als)
	  => (dae3,env''',csets''') &
	list_append(dae1,dae2) => dae5 &
	list_append(dae5,dae3) => dae
	-----------------------------
	inst_classdef(env,mods,pre, csets, Explode.PARTS(els,eqs,als), prot)
	  => (dae,env''', csets''', tys)

  rule	Env.lookup_class(env,cn) => (c,m) &
	(* FIXME order *)
	Mod.merge(mods,m) => mods' &
	Mod.merge(mods',mod) => mods'' &
	inst_class_in(env, mods'', pre, csets, c, prot)
	  => (dae,env, csets', tys)
	----------------------------------------
	inst_classdef(env,mods,pre, csets, Explode.DERIVED(cn,ad,mod), prot)
	  => (dae,env, csets', tys)

  rule	not Env.lookup_class(env,cn) => (_,_) &
	Exp.path_string(cn) => cns &
	print "# unknown class: " & print cns & print "\n"
	----------------------------------------
	inst_classdef(env,mods,pre,csets, Explode.DERIVED(cn,ad,mod), prot)
	  => fail

end

and inst_class_in: (Env, Mod, Prefix, Connect.Sets, Explode.Class, bool)
	  => (DAE.DAEcomp list, Env, Connect.Sets, Types.Var list) =

  rule	print " instantiating " & print n & print "\n" &
	inst_classdef(env,mods,pre,csets,d,prot) => (l,env', csets', tys)
	------------------------------------------------------	
	inst_class_in(env,mods,pre,csets,c as Explode.CLASS(n,false,r,d), prot)
	  => (l,env', csets', tys)

  rule	print "Can't instantiate partial class " & print n & print "\n"
	------------------------------------------------------	
	inst_class_in(env,mod,pre, csets, Explode.CLASS(n,true,_,_), _)
	  => fail

	(* FIXME *)
  rule  print "# Fallthru in inst_class_in\n"
	----------------------------------
	inst_class_in(_,_,_,csets,_,_) => fail

end

and mktype : Types.Var list => Types.Type =

  axiom	mktype [] => Types.BUILTIN("<unknown>")

  axiom	mktype l => Types.COMPLEX(l)

end

and inst_class: (Env, Mod, Prefix, Connect.Sets, Explode.Class)
	  => (DAE.DAEcomp list, Connect.Sets, Types.Type) =

  rule	Env.new_frame(env) => env' &
	inst_class_in(env', mod, pre, csets, c, false) => (dae,_,csets',tys) &
	mktype(tys) => ty
	-------------------------------------------
	inst_class(env, mod, pre, csets, c) => (dae, csets', ty)

end

relation inst_program : (Env, Explode.Program) => DAE.DAEcomp list =

  rule	print "Empty program\n"
	------------------------------
	inst_program(env,[]) => fail

  rule	Env.extend_frame_c(env,c,Mod.NOMOD) => env' &
	inst_class(env',Mod.NOMOD,[],[],c) => (dae1,csets,_) &
	Connect.dump_sets csets &
	Connect.equations csets => dae2 &
	list_append(dae1, dae2) => dae
	-----------------------------
	inst_program(env,[c]) => dae

  rule	Env.extend_frame_c(env,c,Mod.NOMOD) => env' &
	inst_program(env',cs) => l
	----------------------
	inst_program(env,c::cs) => l

end

relation instantiate: Explode.Program => DAE.DAEform =

  rule	Builtin.initial_env => env &
	inst_program(env,p) => l
	-----------------------------
	instantiate(p) => DAE.DAE(l)

end
