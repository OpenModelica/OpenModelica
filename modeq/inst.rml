(* inst.rml *)

module Inst:

  with "exp.rml"
  with "lform.rml"
  with "dae.rml"

  relation instantiate: Lform.Program => DAE.DAEform

end

with "prefix.rml"
with "env.rml"
with "mod.rml"
with "builtin.rml"
with "dump.rml"

type Prefix = Prefix.Prefix



(**********************************************************************)

relation inst_list (* : (Env.Env, Mod.Mod, Prefix, =>, foo list) *)
	 =

  axiom inst_list(env,mod,pre,r,[]) => ([],env)

  rule	r(env,mod,pre,e) => (l,env') &
	inst_list(env',mod,pre,r,es) => (l',env'') &
	list_append(l,l') => l''
	--------------------------------------------
	inst_list(env,mod,pre,r,e::es) => (l'',env'')

end

relation inst_connect: (Exp.Exp,Exp.Exp) => DAE.DAEcomp list =

  rule	print "No connect() yet\n" & fail
	---------------------------------
	inst_connect(_,_) => []

end

relation inst_equation: (Env.Env,Mod.Mod,Prefix,Lform.Equation)
	  => (DAE.DAEcomp list, Env.Env) =

  (* connect
   *
   * Handle connct statements
   *)

  rule	inst_connect(o1,o2) => l
	------------------------
	inst_equation(env,mods,pre,
		      Lform.EQ_EXPR(Exp.CALL(Exp.IDENT("connect"),[o1,o2])))
	 => (l,env)

  (* assert
   *
   * FIXME: Handle assertions
   *)

  rule	Prefix.prefix_exp(e,pre) => e' &
	print " ASSERTION(" & Dump.dump_exp(e') & print "): " &
	print d & print "\n"
	------------------------
	inst_equation(env,mods,pre,
		      Lform.EQ_EXPR(Exp.CALL(Exp.IDENT("assert"),
					     [e,Exp.STRING(d)])))
	  => ([],env)

  rule	print "No expression equations yet\n" & fail
	--------------------------------------------
	inst_equation(env,mods,pre,Lform.EQ_EXPR(_)) => ([],env)

  rule	print " equation\n" &
	Prefix.prefix_exp(e1,pre) => e1' &
	Prefix.prefix_exp(e2,pre) => e2'
	------------------------
	inst_equation(env,mods,pre,Lform.EQ_EQUALS(e1,e2)) =>
		([DAE.EQUATION(Exp.RELATION(e1',Exp.EQUAL,e2'))],env)

end

relation inst_element: (Env.Env,Mod.Mod,Prefix,Lform.Element)
	  => (DAE.DAEcomp list, Env.Env) =

  (* extends
   *
   * Handle "extends" elements by instantiating the class definition
   * of the extended class.
   *)

  rule	Env.lookup_class(env,cn) => c &
	Mod.merge(SOME(mods),SOME(Lform.CLASSMOD(m))) => mods' &
	Mod.simple_maybe_modify_class(env,mods',pre,c)
	  => (Lform.CLASS(n,_,r,d), dae1) &
	(* Can't use inst_class, as that creates a new frame *)
	inst_classdef(env,mods,pre,d) => (dae2,env') &
	list_append(dae1,dae2) => dae
	----------------------------------
	inst_element(env,mods,pre,Lform.ELEMENT(_,_,Lform.EXTENDS(cn,m)))
	  => (dae,env')

  (* components
   *
   * Rules for instantiating components.
   *)

  (* If a variable is declared multiple times, the first is used *)
  rule	Env.lookup_var(env,n) &
	print " ignoring shadowed variable " & print n & print "\n"
	--------------------------
	inst_element(env,mods,pre,
		     Lform.ELEMENT(_,n,Lform.COMPONENT(_,_,_,_,_,_,_,_,_)))
	  => ([],env)

  (* Illegal redeclarations *)
  rule	Env.lookup_var(env,n) &
	print "Trying to redeclare the class " & print n &
	print " as a variable\n" &
	fail
	------------------------
	inst_element(env,mods,pre,Lform.ELEMENT(_,n,Lform.CLASSDEF(_,_)))
	  => ([],env)

  rule	Env.lookup_class(env,Exp.IDENT(n)) => v &
	print "Trying to redeclare the variable " & print n &
	print " as a class\n" &
	fail
	------------------------
	inst_element(env,mods,pre,
		     Lform.ELEMENT(_,n,Lform.COMPONENT(_,_,_,_,_,_,_,_,_)))
	  => ([],env)

  (* A new class definition
   *
   * Put it in the current frame in the environment
   *)
   
  rule	Lform.convert(c) => c' &
	Mod.lookup_modification(mods,n) => mm &
	Mod.simple_maybe_modify_class(env,mm,pre,c') => (c'',dae) &
	Env.extend_frame_c(env,c'') => env'
	----------------------------------------
	inst_element(env,mods,pre,
		     Lform.ELEMENT(_,n,Lform.CLASSDEF(_,c)))
	 => (dae,env')

  (* A variable
   *
   * Lookup the class name, apply specializations and add the variable
   * to the current frame in the environment. Then instantiate the class
   * with an extended prefix.
   *)

  rule	Prefix.prefix_path(Exp.IDENT(n),pre) => vn &

	Exp.path_string(vn) => ns &
	print " variable " & print ns & print "\n" &

	Env.lookup_class(env,t) => cl &

	(* Exp.path_string(t) => ts & *)
	(* print "  class " & print ts & print " found\n" & *)

	(* Find the set of modifications *)
	Mod.lookup_modification(mods,n) => mm &
	Mod.merge(mm,m) => m' &

	Prefix.prefix_add(n,pre) => pre' &

	Mod.maybe_modify_class(env,vn,m',pre',cl) => (cl',dae1) &

	(* print "        modified\n" & *)

	Env.extend_frame_v(env,Env.FRAMEVAR(n,ad,cl',f,p,c,i,o)) => env' &

	(* print "        added\n" & *)

	inst_class(env,Mod.empty_mod,pre',cl') => dae2 &
	list_append(dae1,dae2) => dae
	--------------------------
	inst_element(env,mods,pre,
		     Lform.ELEMENT(_,_,Lform.COMPONENT(f,p,c,i,o,t,n,ad,m)))
         => (dae,env')

  (*
   * If the class lookup in the previous rule fails, this rule catches
   * the error and prints an error message about the unknown class.
   *)

  rule	not Env.lookup_class(env,t) => cl &
	Exp.path_string(t) => s &
	print "# unknown class '" & print s & print "' while instantiating " &
	Prefix.prefix_path(Exp.IDENT(n),pre) => n' &
	Exp.path_string(n') => ns &
	print ns & print "\n"
	--------------------------
	inst_element(env,_,pre,
		     Lform.ELEMENT(_,_,Lform.COMPONENT(_,_,_,_,_,t,n,_,_)))
         => ([],env)

end

and inst_classpart: (Env.Env,Mod.Mod,Prefix,Lform.ClassPart)
	  => (DAE.DAEcomp list,Env.Env) =

  rule	inst_list(env,mods,pre,inst_element,l) => (dae,env')
	-------------------------------------------
	inst_classpart(env,mods,pre,Lform.PUBLIC(l)) => (dae,env')

  rule	inst_list(env,mods,pre,inst_element,l) => (dae,env')
	-------------------------------------------
	inst_classpart(env,mods,pre,Lform.PROTECTED(l)) => (dae,env')

  rule	print "No algorithms yet\n" & fail
	----------------------------------
	inst_classpart(env,mods,pre,Lform.ALGORITHMS(l)) => ([],env)

  rule	inst_list(env,mods,pre,inst_equation,el) => (dae,el')
	------------------------------------
	inst_classpart(env,mods,pre,Lform.EQUATIONS(el)) => (dae,el')

end

and inst_classdef: (Env.Env,Mod.Mod,Prefix,Lform.ClassDef)
	  => (DAE.DAEcomp list, Env.Env) =

  rule	inst_list(env,mods,pre,inst_classpart,pl) => (dae,env')
	------------------------------------------------
	inst_classdef(env,mods,pre,Lform.PARTS(pl)) => (dae,env')

  rule	print "DERIVED not handled\n" & fail
	------------------------------------
	inst_classdef(env,_,_,Lform.DERIVED(_,_,_)) => ([],env)
end

and inst_class: (Env.Env,Mod.Mod,Prefix,Lform.Class) => DAE.DAEcomp list =

  rule	(* print " instantiating " & print n & print "\n" & *)
	Env.new_frame(env) => env' &
	inst_classdef(env',mods,pre,d) => (l,_)
	------------------------------------------------------	
	inst_class(env,mods,pre,c as Lform.CLASS(n,false,r,d)) => l

  rule	print "Can't instantiate partial class " & print n & print "\n" &
	fail
	------------------------------------------------------	
	inst_class(env,mod,pre,Lform.CLASS(n,true,_,_)) => []

  (* FIXME *)

end

relation inst_program : (Env.Env, Absyn.Program) => DAE.DAEcomp list =

  rule	print "Empty program\n" & fail
	------------------------------
	inst_program(env,[]) => []

  rule	Env.extend_frame_c(env,c) => env' &
	inst_class(env',Mod.empty_mod,[],c') => l
	-----------------------------
	inst_program(env,[c]) => l

  rule	Env.extend_frame_c(env,c) => env' &
	inst_program(env',cs) => l
	----------------------
	inst_program(env,c::cs) => l

end

relation instantiate: Lform.Program => DAE.DAEform =

  rule	Builtin.initial_env => env &
	inst_program(env,p) => l
	-----------------------------
	instantiate(p) => DAE.DAE(l)

end
