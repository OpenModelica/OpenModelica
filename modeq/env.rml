(*
    Copyright PELAB, Linkoping University

    This file is part of Open Source Modelica (OSM).

    OSM is free software; you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation; either version 2 of the License, or
    (at your option) any later version.

    OSM is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Foobar; if not, write to the Free Software
    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

*)

(**
 ** file:	 env.rml
 ** module:      Env
 ** description: Environmane management
 **
 ** RCS: $Id$
 **
 ** An environment is a stack of frames, where each frame contains a
 ** number of class and variable bindings.
 **)

module Env:

  with "absyn.rml"
  with "values.rml"
  with "explode.rml"
  with "types.rml"
  with "classinf.rml"
  with "exp.rml"

  type Ident = string

  datatype Frame = FRAME of Ident option *  (* Class name *)
			    (Ident * Item) list * (* List of named items *)
			    bool  (* encapsulated *)
  (* bool=true means that FRAME is created due to encapsulated class  *)



  datatype Item = VAR of Types.Var * SCode.Element option * bool
		| CLASS of SCode.Class * Env
		| TYPE of Types.Type
		| IMPORT of Absyn.Import

  type Env = Frame list

  val empty_not_encapsulated_frame   : Frame
  val empty_encapsulated_frame : Frame

  
  val empty_env   : Env

  relation open_scope       : (Env,bool) => Env
  relation name_scope: (Env,Ident) => Env 
  relation extend_frame_c   : (Env, SCode.Class) => Env
  relation extend_frame_classes   : (Env, SCode.Program) => Env
  relation extend_frame_v   : (Env, Types.Var,SCode.Element option,bool) => Env
  relation update_frame_v   : (Env, Types.Var,bool) => Env
  relation extend_frame_t : (Env,Ident,Types.Type) => Env
  relation extend_frame_i : (Env, Absyn.Import) => Env
  relation top_frame : Env => Frame
  relation get_env_path: (Env) => Absyn.Path option
  relation print_env_path: (Env) => ()
  relation print_env        : Env => ()
	  
  relation print_env_graphviz : (Env * string) => ()

  relation print_frame      : Frame => ()

end



with "dump.rml"
with "graphviz.rml"
with "dae.rml"
with "print.rml"

(** - Values *)

val empty_not_encapsulated_frame = FRAME(NONE,[],false)  
val empty_encapsulated_frame = FRAME(NONE,[],true)
val empty_env = []

(** - Relations *)

(** relation: open_scope
 **
 ** Opening a new scope in the environment mans adding a new frame on
 ** top of the stack of frames.
 **)

relation open_scope: (Env,bool) => Env =

  axiom	open_scope(env,encflag) =>  FRAME(NONE,[],encflag)::env

(*  rule
	Print.print_buf "\nOpens scope."
	-----------
	open_scope(env,encflag) => FRAME([],encflag)::env
*)
end

(** relation: name_scope
 **
 ** This relation names the current scope, giving it an identifier.
 ** Scopes needs to be named for several reasons. First, it is needed for debugging purposes, since
 ** it is easier to follow the environment if we know what the current class being instantiated is.
 **
 ** Secondly, it is needed when expanding type names in the context of flattening of the inheritance
 ** hierarchy. The reason for this is that types of inherited components needs to be expanded such that ** the types can be looked up from the environment of the base class.
 **)
 
relation name_scope: (Env,Ident) => Env =

  axiom	name_scope(FRAME(_,items,encflag)::res,id) => FRAME(SOME(id),items,encflag)::res

end

(** relation: extend_frame_c
 **
 ** This relation adds a class definition to the environment.
 **)

relation extend_frame_c : (Env, SCode.Class) => Env =

(*
  rule	Print.print_buf "extend_frame_c:\n" &
	print_env env &
	Print.print_buf " + " &
	Print.print_buf n &
	Print.print_buf " = " &
	print_env ((FRAME((n,CLASS(c,env))::items,encflag)::fs)) &
	Print.print_buf "\n" 
	----------------
	extend_frame_c(env as (FRAME(id,items,encflag)::fs),c as SCode.CLASS(n,_,_,_))
	  => ((FRAME(id,(n,CLASS(c,env))::items,encflag)::fs))
*)

  axiom	extend_frame_c(env as (FRAME(id,items,encflag)::fs),c as SCode.CLASS(n,_,_,_,_))
	  => ((FRAME(id,(n,CLASS(c,env))::items,encflag)::fs))

end

relation extend_frame_classes : (Env, SCode.Program) => Env =

  axiom	extend_frame_classes(env,[]) => env

  rule	extend_frame_c(env,c) => env' &
	extend_frame_classes(env',cs) => env''
	-------------------------------------
	extend_frame_classes(env,c::cs) => env''
end

(** relation: extend_frame_v
 **
 ** This relation adds a component to the environment.
 **)

relation extend_frame_v : (Env,Types.Var,SCode.Element option,bool) => Env =
	
(*
  rule	Print.print_buf "extend_frame_v:\n" &
   print_env env &
   Print.print_buf " + " &
   Print.print_buf n &
	Print.print_buf " = " &
   print_env (FRAME(id,(n,VAR(v))::items,encflag)::fs) &
   Print.print_buf "\n" 
   ----------------
   extend_frame_v(env as (FRAME(id,items,encflag)::fs),v as Types.VAR(n,_,_,_,_))
   => (FRAME(id,(n,VAR(v))::items,encflag)::fs)
 *)
	
  axiom extend_frame_v(FRAME(id,items,encflag)::fs,v as Types.VAR(n,_,_,_,_),c,i)
	    => (FRAME(id,(n,VAR(v,c,i))::items,encflag)::fs)
	    
end
	
(** relation: update_frame_v
 **
 ** This relation updates a component already added to the environment, but that prior to the
 ** update did not have any binding. (I.e this relation is called in the second stage of 
 **  instantiation with declare before use.)
**)
	
	
relation update_frame_v : (Env,Types.Var,bool) => Env =
	
  rule	v = n
	---------------------------	
	update_frame_v(FRAME(sid,(id,VAR(Types.VAR(v,_,_,_,_),c,_))::rest,encflag)::rest2,v2 as Types.VAR(n,_,_,_,_),i) 
	    => (FRAME(sid,(id,VAR(v2,NONE,i))::rest,encflag)::rest2)
  rule	not v = n &
	update_frame_v(FRAME(sid,rest,encflag)::rest2,v2,i) => FRAME(sid,res,encflag)::reslst
	-------------------------------------------
	update_frame_v(FRAME(sid,(id,VAR(v1 as Types.VAR(v,_,_,_,_),c,i1))::rest,encflag)::rest2,v2 as Types.VAR(n,_,_,_,_),i)
	    => FRAME(sid,(id,VAR(v1,c,i1))::res,encflag)::reslst
	
  rule	update_frame_v(FRAME(sid,rest,encflag)::rest2,v2,i) => FRAME(sid,res,encflag)::reslst
	-------------------------------------------
	update_frame_v(FRAME(sid,(id,notvar)::rest,encflag)::rest2,v2 as Types.VAR(n,_,_,_,_),i)
	    => FRAME(sid,(id,notvar)::res,encflag)::reslst

  rule	(* Also check frames above, e.g. when variable is in base class *)
	update_frame_v(fs,v,i) => frames
	--------------
	update_frame_v(FRAME(sid,[],encflag)::fs,v as Types.VAR(n,_,_,_,_),i)
	  => (FRAME(sid,[],encflag)::frames)

  rule	Print.print_buf "- update_frame_v, variable " & Print.print_buf n &
	Print.print_buf " not found\n rest of env:" &
	print_env fs & Print.print_buf "\n" 
	--------------
	update_frame_v(FRAME(sid,[],encflag)::fs,v as Types.VAR(n,_,_,_,_),_)
	  => (FRAME(sid,[],encflag)::fs)

  rule	Print.print_buf "- update_frame_v failed\n" &
	Print.print_buf "  - variable: " & Types.print_var v &
	Print.print_buf "\n" 
	----------------
	update_frame_v(_,v as Types.VAR(id,_,_,_,_),_) => fail
end
	
(** relation: extend_frame_t
 **
 ** This relation adds a type to the environment.  Types in the
 ** environment are used for looking up constants etc. inside class
 ** definitions, such as packages.  For each type in the environment,
 ** there is a class definition with the same name in the
 ** environment.
 **)
		 
relation extend_frame_t : (Env,Ident,Types.Type) => Env =

(*
  rule	Print.print_buf "extend_frame_t:\n" &
	print_env env &
	Print.print_buf " + " &
	Print.print_buf n &
	Print.print_buf " = " &
	print_env (FRAME(sid,(n,TYPE(t))::items,encflag)::fs) &
	Print.print_buf "\n" 
	----------------
	extend_frame_t(env as (FRAME(sid,items,encflag)::fs), n, t)
	  => (FRAME(sid,(n,TYPE(t))::items,encflag)::fs)
*)

  axiom extend_frame_t(FRAME(sid,items,encflag)::fs, n, t)
	  => (FRAME(sid,(n,TYPE(t))::items,encflag)::fs)

end

relation extend_frame_i : (Env,Absyn.Import) => Env =

  axiom extend_frame_i(FRAME(sid,items,encflag)::fs,imp)
	  => (FRAME(sid,("",IMPORT(imp))::items,encflag)::fs)
end

relation top_frame : Env => Frame =
  axiom	top_frame([fr]) => fr
  rule	top_frame(lst) => fr
	--------------------
	top_frame(elt::(lst as (_::_))) => fr
end

(** relation: get_env_path
 **
 ** This relation returns all partially instantiated parents as an Absyn.Path option
 ** I.e. it collects all identifiers of each frame until it reaches the topmost
 ** unnamed frame. If the environment is only the topmost frame, NONE is returned.
**)
relation get_env_path: Env => Absyn.Path option =

  axiom	get_env_path ([FRAME(SOME(id),_,_),FRAME(NONE,_,_)]) => SOME(Absyn.IDENT(id))
	
  rule	get_env_path(rest) => SOME(path) &
	Absyn.join_paths(path,Absyn.IDENT(id)) => path'
	----------------------------------------------
	get_env_path(FRAME(SOME(id),_,_)::rest) =>SOME(path')

  axiom	get_env_path(_) => NONE
end

relation print_env_path: Env => () =

  rule	get_env_path(env) => SOME(path) &
	Absyn.path_string(path) => pathstr &
	Print.print_buf pathstr
	---------------
	print_env_path(env) => ()

  rule	Print.print_buf "TOPENV"
	---------------
	print_env_path(env) => ()

end
relation print_env : Env => () =

  rule	Print.print_buf "Empty env\n"
	-------------------
	print_env []

  rule	print_frame fr &
	print_env frs
	------------
	print_env fr::frs

end


relation print_frame : Frame => () =

  rule	Print.print_buf "FRAME: " &
	Print.print_buf sid &
	Print.print_buf " (enc=" &
        Dump.print_bool  encflag  &
        Print.print_buf ") " &
	print_frame_contents cont &
	Print.print_buf "\n"
	--------------------------------------------
	print_frame FRAME(SOME(sid),cont,encflag)

  rule	Print.print_buf "FRAME: " &
	Print.print_buf "unnamed" &
	Print.print_buf " (enc=" &
        Dump.print_bool  encflag  &
        Print.print_buf ") " &
	print_frame_contents cont &
	Print.print_buf "\n"
	--------------------------------------------
	print_frame FRAME(NONE,cont,encflag)

end


relation print_frame_contents: (Ident * Item) list => () =

  axiom	print_frame_contents [] 

  rule	print_frame_element e
	-------------------------
	print_frame_contents [e as (n,i)]

  rule	print_frame_element e & Print.print_buf "," & print_frame_contents rst
	------------------------------------------------
	print_frame_contents ((e as (n,i))::rst)
end


relation print_frame_element: (Ident * Item) => () =

  rule	Print.print_buf "v:" & Print.print_buf n (*& Print.print_buf "(" &  SCode.print_element elt & Print.print_buf ") ["  &
	Types.print_type tp & Print.print_buf "]" & Print.print_buf "{" & Types.print_var tv & Print.print_buf "}" *)
	-------
	print_frame_element ((n,VAR(tv as Types.VAR(_,_,_,tp,_),SOME(elt),i)))

  rule	Print.print_buf "v:" & Print.print_buf n (*& Print.print_buf "=" & Types.print_binding bnd & Print.print_buf "\n" *)
	-------
	print_frame_element ((n,VAR(Types.VAR(_,_,_,_,bnd),NONE,i)))

  rule	Print.print_buf "c:" & Print.print_buf n
	-------
	print_frame_element ((n,CLASS(_,_)))

  rule	(*Print.print_buf "t:" & Print.print_buf n*)
	-------
	print_frame_element ((n,TYPE(_)))

  rule	Print.print_buf "imp:" & Dump.print_import imp 
	-------
	print_frame_element ((n,IMPORT(imp)))
end


relation print_env_graphviz : (Env * string) => () =

  rule	build_env_graphviz ((env,str)) => r &
	Graphviz.dump(r)
	----------------
	print_env_graphviz ((env,str))

end

relation build_env_graphviz : (Env * string) => Graphviz.Node =

  rule	string_append ("ROOT: ", str) => str' &
	build_env_graphviz_2 env => nodelist
	------------------------------------
	build_env_graphviz ((env,str)) => Graphviz.NODE(str',[],nodelist)

end

relation build_env_graphviz_2 : Env => Graphviz.Node list =

  axiom	build_env_graphviz_2 [] => []

  rule	build_env_graphviz_2 rest => nodelist &
	build_frame_graphviz frame => node
	----------------------------------
	build_env_graphviz_2 frame::rest => node::nodelist

end

relation is_var_item = 
  axiom	is_var_item ((_,VAR(_,_,_))) 
end

relation is_class_item = 
  axiom	is_class_item ((_,CLASS(_,_))) 
end

relation is_type_item = 
  axiom	is_type_item ((_,TYPE(_))) 
end

relation build_frame_graphviz : Frame => Graphviz.Node =

  rule	build_item_listnode (itemlist, is_var_item,"VARS") => varsnode &
	build_item_listnode (itemlist, is_class_item,"CLASSES") => classesnode &
	build_item_listnode (itemlist, is_type_item,"TYPES") => typesnode &
	let nodelist = [varsnode,classesnode,typesnode]
	-------------------------------------------
	build_frame_graphviz FRAME(sid,itemlist,encflag) => Graphviz.NODE("FRAME",[],nodelist)
end

relation build_item_listnode : ((Ident * Item) list, (Ident * Item) => (), string) 
	  => Graphviz.Node =

  rule	DAE.get_matching (items, cond) => selitems &
	build_item_listnode_2 (selitems, 1) => node
	--------------------------------------------------------
	build_item_listnode (items, cond, name) 
	  => Graphviz.NODE(name, [], [node])

end

relation build_item_listnode_2 : ((Ident * Item) list, int) 
	  => Graphviz.Node =

  rule	DAE.build_gr_strlist (items, build_item_str, 10) => (strlist, []) &
	int_string count => cstr
	--------------------------------------------------------
	build_item_listnode_2 (items, count) 
	  => Graphviz.LNODE(cstr, strlist, [Graphviz.box], [])

  rule	DAE.build_gr_strlist (items, build_item_str, 10) => (strlist, ignored) &
	int_string count => cstr &
	int_add (count, 1) => count' &
	build_item_listnode_2 (ignored, count') => restnode 
	--------------------------------------------------------
	build_item_listnode_2 (items, count) 
	  => Graphviz.LNODE(cstr, strlist, [Graphviz.box], [restnode])

end

relation build_item_str : (Ident * Item) => string =

  rule	string_append ("VAR: ",id) => s
	------------------------------
	build_item_str ((id, VAR(_,_,_))) => s

  rule	(*build_env_graphviz env => r &*)
	string_append ("CLASS: ", id) => s
	----------------------------------
	build_item_str ((id, CLASS(cls, env))) => s

  rule	string_append ("TYPE: ", id) => s
	---------------------------------
	build_item_str ((id, TYPE(_))) => s

end

(*
relation build_item_graphviz : (Ident, Item) => Graphviz.Node =

  axiom	build_item_graphviz (id, VAR(_)) => Graphviz.LNODE("VAR",[id],[],[])

  axiom	build_item_graphviz (id, CLASS(cls, env)) => Graphviz.LNODE("CLASS",[id],[],[])

  axiom	build_item_graphviz (id, TYPE(_)) => Graphviz.LNODE("TYPE",[id],[],[])

end
*)
