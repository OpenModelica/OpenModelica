(*
    Copyright PELAB, Linkoping University

    This file is part of Open Source Modelica (OSM).

    OSM is free software; you can redistribute it and/or modify
    it under the terms of the GNU General Public License as published by
    the Free Software Foundation; either version 2 of the License, or
    (at your option) any later version.

    OSM is distributed in the hope that it will be useful,
    but WITHOUT ANY WARRANTY; without even the implied warranty of
    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
    GNU General Public License for more details.

    You should have received a copy of the GNU General Public License
    along with Foobar; if not, write to the Free Software
    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

*)

(**
 ** file:        mod.rml
 ** module:      Mod
 ** description: Modification handling
 **
 ** RCS: $Id$
 **
 ** Modifications are simply the same kind of modifications used in
 ** the `Absyn' module.
 **
 **)

module Mod:

  with "absyn.rml"
  with "dae.rml"
  with "env.rml"
  with "exp.rml"
  with "prefix.rml"
  with "explode.rml"
  with "types.rml"
  with "staticexp.rml"
  with "values.rml"

  type Ident = string

  (** - Modification datatype *)
  datatype EqMod = TYPED of Exp.Exp * Types.Properties |
		   UNTYPED of Absyn.Exp
  datatype SubMod = NAMEMOD of Ident * Mod
		  | IDXMOD of int list * Mod
  and Mod = MOD of bool * (SubMod list) * EqMod option
  	  | REDECL of bool * SCode.Element list
	  | NOMOD
  (** This type is very similar to `SCode.Mod'.  The main difference is
   ** that it uses `Exp.Exp' for the expressions.  Expressions stored
   ** here are prefixed and typechecked.
   **)

  relation elab_mod : (Env.Env, Prefix.Prefix, SCode.Mod) => Mod
  relation unelab_mod : (Mod) => SCode.Mod
  relation update_mod: (Env.Env, Prefix.Prefix, Mod) => Mod 
  relation elab_untyped_mod : (SCode.Mod) => Mod

  relation lookup_comp_modification: (Mod, Absyn.Ident) => Mod
  relation lookup_idx_modification: (Mod, int) => Mod
  relation lookup_modification_p: (Mod,Absyn.Path) => Mod
  relation merge : (Mod, Mod) => Mod
  relation mod_equation : Mod => EqMod option

  relation print_mod : Mod => ()

end


with "dump.rml"
with "debug.rml"
with "print.rml"
with "inst.rml"

(** relation: elab_mod
 **
 ** This relation elaborates on the expressions in a modification and
 ** turns them into global expressions.  This is done because the
 ** expressions in modifications must be elaborated on in the context
 ** they are provided in, and not the context they are used in.
 **)
 
relation elab_mod : (Env.Env, Prefix.Prefix, SCode.Mod) => Mod =

  axiom	elab_mod (_,_,SCode.NOMOD) => NOMOD

  rule	elab_submods (env,pre,subs) => subs'
	------------------------------------
	elab_mod (env,pre,m as SCode.MOD(final,subs,NONE))
	  => MOD(final,subs',NONE)
	
  rule	elab_submods (env,pre,subs) => subs' &
	Static.elab_exp(env,e,false,NONE) => (e',prop,_) &
	Prefix.prefix_exp(env,e',pre) => e''
	------------------------------------
	elab_mod (env,pre,m as SCode.MOD(final,subs,SOME(e)))
	  => MOD(final,subs',SOME(TYPED(e'',prop)))

  axiom	elab_mod (env, pre, m as SCode.REDECL(final, elist)) 
	  =>  REDECL(final, elist)

  rule	Debug.fprint("failtrace", "#-- elab_mod failed\n")
	-------------------------------------------------
	elab_mod (_,pre,mod) => fail 
end



relation unelab_mod: Mod => SCode.Mod =
	
  axiom	unelab_mod (NOMOD) => SCode.NOMOD
	
  rule	unelab_submods (subs) => subs'
	------------------------------------
	unelab_mod (m as MOD(final,subs,NONE))
	  =>  SCode.MOD(final,subs',NONE)
	  
  rule	unelab_submods (subs) => subs' 
	------------------------------------
	unelab_mod (m as MOD(final,subs,SOME(UNTYPED(e))))
	  => SCode.MOD(final,subs',SOME(e))
	  
  rule	unelab_submods (subs) => subs' &
	Exp.unelab_exp(e) => e'
	------------------------------------
	unelab_mod (m as MOD(final,subs,SOME(TYPED(e,p))))
	  => SCode.MOD(final,subs',SOME(e'))
	  
  axiom	unelab_mod (m as REDECL(final, elist)) 
	  =>  SCode.REDECL(final, elist)
	  
  rule	Print.print_buf "#-- elab_untyped_mod failed:\n" &
	print "- unelab_mod failed\n" 
	----------
	unelab_mod (mod) => fail
end


relation unelab_submods : ( SubMod list)
	  => SCode.SubMod list =
	  
  axiom	unelab_submods ([]) => []
	
  rule	unelab_submod (x) => x' &
	unelab_submods (xs) => xs' &
	list_append(x',xs') => res	    
	-----------------------------
	unelab_submods (x::xs) => res
end

(** relation: unelab_submod
 **
 ** This relation unelaborates on a submodification.
 **)
		 
relation unelab_submod : (SubMod) => SCode.SubMod list =
	
  rule	unelab_mod (m) => m'
	--------------------------
	unelab_submod (NAMEMOD(i,m)) => [SCode.NAMEMOD(i,m')]
	
  rule	unelab_subscript(ss) => ss' &
	unelab_mod(m) => m'
	---------------------------
	unelab_submod (IDXMOD(ss,m)) => [SCode.IDXMOD(ss',m')]
end

relation unelab_subscript: (int list) => SCode.Subscript list =
		    
   axiom unelab_subscript ([]) => []

   rule	unelab_subscript(is) => xs 
	--------------------------
	unelab_subscript (i::is) => Absyn.SUBSCRIPT(Absyn.INTEGER(i))::xs	    
end
(** relation: update_mod
 **
 ** This relation updates and untyped modification to a typed one, by looking
 ** up the type of the modifier in the environment and update it.
 **)
relation update_mod: (Env.Env,Prefix.Prefix, Mod) => Mod =

  axiom	update_mod(_,_,NOMOD) => NOMOD
  axiom	update_mod(_,_,m as REDECL(_,_)) => m

  rule	update_submods (env,pre,subs) => subs' &
	Static.elab_exp(env,e,false,NONE) => (e',prop,_)  &
	Prefix.prefix_exp(env,e',pre) => e'' 
	-----------------
	update_mod(env, pre, m as MOD(f, subs, SOME(UNTYPED(e))))
	  => MOD(f, subs', SOME(TYPED(e'', prop)))

  rule	update_submods (env, pre, subs) => subs' 
	---------------------------------------
	update_mod(env, pre, MOD(f, subs, SOME(TYPED(e, p)))) 
	  => MOD(f, subs', SOME(TYPED(e, p)))

  rule	update_submods (env,pre,subs) => subs' 	
	-------------------------
	update_mod(env, pre, MOD(f, subs, NONE)) => MOD(f,subs',NONE)

  rule	Print.print_buf "- update_mod failed\n mod:" &
	print_mod m & Print.print_buf "\n" 
	--------------
	update_mod(env,pre,m) => fail
end

relation update_submods: (Env.Env, Prefix.Prefix, SubMod list)
	=> SubMod list =
  axiom	update_submods (_,_,[]) => []

  rule	update_submod (env,pre,x) => x' &
	update_submods (env,pre,xs) => xs' &
	insert_submods (x',xs') => res
	-----------------------------
	update_submods (env,pre,x::xs) => res
end


relation update_submod : (Env.Env, Prefix.Prefix, SubMod) 
	  => SubMod list =

  rule	update_mod (env,pre,m) => m'
	--------------------------
	update_submod (env,pre,NAMEMOD(i,m)) => [NAMEMOD(i,m')]

  rule	(*Static.elab_subscripts (env,ss) => (ss',true) &*)
	update_mod (env,pre,m) => m' &
	make_idxmods ([],m') => smods &
	Print.print_buf "#Error, not implemented updating of IDXMOD yet\n"
	(* Need to store untyped IDXMOD list in mods such that we can elab *)
	(* the index here *)
	---------------------------
	update_submod (env,pre,IDXMOD(_,m)) => smods
end

(** relation elab_untyped_mod
 ** This relation is used to convert SCode.Mod into Mod, without 
 ** adding correct type information. Instead, a undefined type will be 
 ** given to the modification. This is used when modifications of e.g. 
 ** elements in base classes used. For instance,
 ** model test extends A(x=y); end test; // both x and y are defined in A
 ** The modifier x=y must be merged with outer modifiers, thus it needs 
 ** to be converted to Mod.
 ** Notice that the correct type information must be updated later on.
 **)

relation elab_untyped_mod: (SCode.Mod) => Mod =

  axiom	elab_untyped_mod (SCode.NOMOD) => NOMOD

  rule	elab_untyped_submods (subs) => subs'
	------------------------------------
	elab_untyped_mod (m as SCode.MOD(final,subs,NONE))
	  => MOD(final,subs',NONE)
	
  rule	elab_untyped_submods (subs) => subs' 
	------------------------------------
	elab_untyped_mod (m as SCode.MOD(final,subs,SOME(e)))
	  => MOD(final,subs',SOME(UNTYPED(e)))

  axiom	elab_untyped_mod (m as SCode.REDECL(final, elist)) 
	  =>  REDECL(final, elist)

  rule	Print.print_buf "#-- elab_untyped_mod failed:\n" &
	SCode.print_mod mod &
	Print.print_buf "\n"
	----------
	elab_untyped_mod (mod) => fail
end

(** relation: elab_submods
 **
 ** This relation helps `elab_mod' by recusively elaborating on a list
 ** of submodifications.
 **)

relation elab_submods : (Env.Env, Prefix.Prefix, SCode.SubMod list)
	  => SubMod list =

  axiom	elab_submods (_,_,[]) => []

  rule	elab_submod (env,pre,x) => x' &
	elab_submods (env,pre,xs) => xs' &
	insert_submods (x',xs') => res
	-----------------------------
	elab_submods (env,pre,x::xs) => res

end

(** relation: elab_submod
 **
 ** This relation elaborates on a submodification, turning an
 ** `SCode.SubMod' into one or more `SubMod's.
 **)

relation elab_submod : (Env.Env, Prefix.Prefix, SCode.SubMod) => SubMod list =

  rule	elab_mod (env,pre,m) => m'
	--------------------------
	elab_submod (env,pre,SCode.NAMEMOD(i,m)) => [NAMEMOD(i,m')]

  rule	Static.elab_subscripts (env,ss) => (ss',true) &
	elab_mod (env,pre,m) => m' &
	make_idxmods (ss',m') => smods
	---------------------------
	elab_submod (env,pre,SCode.IDXMOD(ss,m)) => smods

end

(** relation: elab_untyped_submods
 **
 ** This relation helps `elab_untyped_mod' by recusively elaborating on a list
 ** of submodifications.
 **)

relation elab_untyped_submods : (SCode.SubMod list)
	  => SubMod list =

  axiom	elab_untyped_submods ([]) => []

  rule	elab_untyped_submod (x) => x' &
	elab_untyped_submods (xs) => xs' &
	insert_submods (x',xs') => res
	-----------------------------
	elab_untyped_submods (x::xs) => res
end

(** relation: elab_untyped_submod
 **
 ** This relation elaborates on a submodification, turning an
 ** `SCode.SubMod' into one or more `SubMod's, wihtout type information.
 **)

relation elab_untyped_submod : (SCode.SubMod) => SubMod list =

  rule	elab_untyped_mod (m) => m'
	--------------------------
	elab_untyped_submod (SCode.NAMEMOD(i,m)) => [NAMEMOD(i,m')]

  rule	elab_untyped_mod (m) => m'
	---------------------------
	elab_untyped_submod (SCode.IDXMOD(subcr,m))
	  => [IDXMOD([-1],m')]
end

(** relation: make_idxmods
 **
 ** From a list of list of integers, this relation creates a list of
 ** sub-modifications of the `IDXMOD' variety.
 **)

relation make_idxmods : (Exp.Subscript list, Mod) => SubMod list =

  axiom	make_idxmods([Exp.INDEX(Exp.ICONST(x))],m) => [IDXMOD([x],m)]

  rule	make_idxmods(xs, m) => mods &
	prefix_idxmods(mods, x) => mods'
	--------------------------------
	make_idxmods(Exp.INDEX(Exp.ICONST(x))::xs, m) => mods'

  rule	Print.print_buf "= expand_slice\n" &
	expand_slice(x, xs, 1, m) => mods
	---------------------------
	make_idxmods(Exp.SLICE(Exp.ARRAY(_,_,x))::xs, m) => mods

  rule	Print.print_buf "# Sorry, [:] slices are not handled in modifications\n"
	--------------------------------------------------------------
	make_idxmods(Exp.WHOLEDIM::xs, m) => fail
	
end

(** relation: prefix_idxmods
 **
 ** This relation adds a subscript to each `IDXMOD' in a list of
 ** submodifications.
 **)

relation prefix_idxmods : (SubMod list, int) => SubMod list =

  axiom	prefix_idxmods([],_) => []

  rule	prefix_idxmods(mods, i) => mods'
	--------------------------------
	prefix_idxmods(IDXMOD(l,m)::mods, i) => IDXMOD(i::l,m)::mods'

end

(** relation: expand_slice
 **
 ** This relation goes through an array slice modification and creates
 ** an singly indexed modification for each index in the slice.  For
 ** example, `x[2:3] = y' is changed into `x[2] = y[1]' and
 ** `x[3] = y[2]'.
 **)

relation expand_slice : (Exp.Exp list, Exp.Subscript list, int, Mod)
	  => SubMod list =

  axiom	expand_slice([], _, _, _) => []

  axiom	expand_slice([], _, _, _) => []

  rule	Exp.simplify(Exp.ASUB(e,n)) => e' &
	Types.unlift_array(t) => t' &
	make_idxmods(Exp.INDEX(x)::ss,
		     MOD(final, [],
			 SOME(TYPED(e',Types.PROP(t',const))))) => mods1 &
	int_add(n, 1) => n' &
	expand_slice(xs, ss, n', m) => mods2 &
	list_append(mods1, mods2) => mods
	---------------------------------
	expand_slice(x::xs, ss, n,
		     m as MOD(final, [],
			      SOME(TYPED(e,Types.PROP(t,const))))) => mods

  rule	Print.print_buf "# Illegal modification (" & print_mod mod & Print.print_buf ")\n"
	--------------------------------
	expand_slice(_,_,_,mod) => fail

end

(** relation: expand_list
 **
 ** This utility relation takes a list of integer values and a list of
 ** list of integers, and for each integer in the first and each list
 ** in the second list creates a
 ** list with that integer as head and the second list as tail. All
 ** resulting lists are collected in a list and returned.
 **)

relation expand_list : (Values.Value list, int list list) => int list list =

  axiom	expand_list([],_) => []

  axiom	expand_list(_,[]) => []

  rule	expand_list(xx,ys) => l1 &
	expand_list(xs,yy) => l2 &
	list_append(l1,l2) => l
	-----------------------
	expand_list(xx as (Values.INTEGER(x)::xs),yy as (y::ys))
		    => ((x::y)::l)

end

(** relation: insert_submods
 **
 ** This relation repeatedly calls `insert_submod' to incrementally
 ** insert several sub-modifications.
 **)

relation insert_submods : (SubMod list, SubMod list) => SubMod list =

  axiom	insert_submods ([],_) => []

  rule	insert_submod (x,y) => x' &
	insert_submods (xs,y) => xs' &
	list_append(x', xs') => l
	-------------------------
	insert_submods (x::xs,y) => l

end

(** relation: insert_submod
 **
 ** This relation inserts a `SubMod' into a list of unique `SubMod's,
 ** while keeping the uniqueness, merging the submod if necessary.
 **)

relation insert_submod : (SubMod, SubMod list) => SubMod list =

  axiom	insert_submod (sub,[]) => [sub]

  rule	n1 = n2 &
	merge (m1,m2) => m
	----------------------
	insert_submod (NAMEMOD(n1,m1),NAMEMOD(n2,m2)::tail)
	  => (NAMEMOD(n1,m)::tail)

  rule	i1 = i2 &
	merge (m1,m2) => m
	----------------------
	insert_submod (IDXMOD(i1,m1),IDXMOD(i2,m2)::tail)
	  => (IDXMOD(i1,m)::tail)

  axiom	insert_submod (sub1, sub2) => sub1::sub2

end

(**
 ** - Lookup
 **)

(** relation: lookup_modification_p
 ** 
 ** This relation extracts a modification from inside another
 ** modification, using a name to look up submodifications.
 **)

relation lookup_modification_p: (Mod,Absyn.Path) => Mod =

  rule	lookup_comp_modification (m,n) => mod
	--------------------------------
	lookup_modification_p (m,Absyn.IDENT(n)) => mod

  rule	lookup_comp_modification (m,n) => mod &
	lookup_modification_p (mod,p) => mod'
	-------------------------------------
	lookup_modification_p (m,Absyn.QUALIFIED(n,p)) => mod'

  rule	Print.print_buf "- lookup_modification_p failed\n" 
	-----------------------
	lookup_modification_p(_,_) => fail
end

(** relation: lookup_comp_modification
 **
 ** This relation is used to look up an identifier in a modification.
 **)

relation lookup_comp_modification: (Mod, Absyn.Ident) => Mod =

  axiom lookup_comp_modification(NOMOD,_) => NOMOD

  axiom	lookup_comp_modification(REDECL(_,_),_) => NOMOD

  rule  lookup_comp_modification2(subs,n) => mod
	-----------------------------------
	lookup_comp_modification(MOD(_,subs,_),n) => mod
	
  rule	Print.print_buf "# Mod.lookup_comp_modification(" &
	print_mod mod & Print.print_buf "," & Print.print_buf i &
	Print.print_buf ") failed\n" &
	print "- lookup_comp_modification failed\n" 
	--------------------
	lookup_comp_modification(mod,i) => fail

end	

(** relation: lookup_comp_modification2
 ** 
 ** This relation is just a helper to `lookup_comp_modification'.
 **)

relation lookup_comp_modification2 : (SubMod list,Absyn.Ident) => Mod =

  axiom lookup_comp_modification2([],_) => NOMOD

  rule  n = m
	--------------------------------------
	lookup_comp_modification2(NAMEMOD(n,mod)::_,m) => mod
	
  rule  lookup_comp_modification2(xs,n) => mod
	---------------------------------
	lookup_comp_modification2(x::xs,n) => mod

  rule	Print.print_buf "- lookup_comp_modification2 failed\n"
	---------------------------------------
	lookup_comp_modification2 (_,_) => fail

end

(** relation: lookup_idx_modification
 **
 ** This relation extracts modifications to an array element, using an
 ** integer to index the modification.
 **)

relation lookup_idx_modification: (Mod, int) => Mod =

  axiom lookup_idx_modification(NOMOD,_) => NOMOD

  axiom	lookup_idx_modification(REDECL(_,_),_) => NOMOD

  rule  lookup_idx_modification2(subs,NONE,idx) => (mod',subs') &
	merge (MOD(f,subs',NONE),mod') => mod'' &

	index_eqmod (eq,[idx]) => eq' &
	merge (mod'',MOD(false,[],eq')) => mod'''
	-----------------------------------
	lookup_idx_modification(MOD(f,subs,eq),idx) => mod'''
	
  rule	Print.print_buf "# Mod.lookup_idx_modification(" &
	print_mod mod & Print.print_buf "," & int_string idx => s & Print.print_buf s &
	Print.print_buf ") failed\n"
	--------------------
	lookup_idx_modification(mod,idx) => fail

end	

(** relation: lookup_idx_modification2
 **
 ** This relation does part of the job for `lookup_idx_modification'.
 **)

relation lookup_idx_modification2 : (SubMod list, EqMod option, int)
	  => (Mod, SubMod list) =

  axiom lookup_idx_modification2([],_,_) => (NOMOD, [])

	(* FIXME: Redeclaration *)

  rule  x = y &
	lookup_idx_modification2(subs,eq,y) => (NOMOD,subs')
	--------------------------------------
	lookup_idx_modification2(IDXMOD([x],mod)::subs,eq,y)
	  => (mod,subs')
	
  rule  x = y &
	lookup_idx_modification2(subs,eq,y) => (mod',subs')
	---------------------------------
	lookup_idx_modification2(IDXMOD(x::xs,mod)::subs,eq,y)
	  => (mod',IDXMOD(xs,mod)::subs')

  rule  not x = y &
	lookup_idx_modification2(subs,eq,y) => (mod',subs')
	---------------------------------
	lookup_idx_modification2(IDXMOD(x::xs,mod)::subs,eq,y)
	  => (mod',subs')

  rule  lookup_idx_modification2(xs,eq,idx) => (mod,xs')
	---------------------------------
	lookup_idx_modification2(x::xs,eq,idx) => (mod,x::xs')

  rule	Print.print_buf "- lookup_idx_modification2 failed\n"
	---------------------------------------
	lookup_idx_modification2 (_,_,_) => fail

end

(** relation: index_eqmod
 **
 ** If there is an equation modification, this relation can subscript
 ** it using the provided indexing expressions.  This is used when a
 ** modification equates an array variable with an array expression.
 ** This expression will be expanded to produce one equation
 ** expression per array component.
 **)

relation index_eqmod : (EqMod option, int list) => EqMod option =

  axiom	index_eqmod (NONE,_) => NONE

  axiom	index_eqmod (e,[]) => e

  rule	Types.unlift_array t => t' &
	Exp.simplify Exp.ASUB(e,x) => exp &
	index_eqmod (SOME(TYPED(exp,Types.PROP(t',c))),xs) => e
	------------------------------------------
	index_eqmod (SOME(TYPED(e,Types.PROP(t,c))),x::xs) => e

end

(**
 **
 ** - Merging
 **
 ** The merge relation merges to modifications to one. The first
 ** argument is the "outer" modification that should take precedence over
 ** the "inner" modifications.
 **
 **)

(** relation: merge
 ** 
 ** This relation merges to modificiations into one.  The first
 ** modifications takes precedence over the second.
 **)

relation merge : (Mod, Mod) => Mod =

  axiom merge(NOMOD,NOMOD) => NOMOD

  axiom merge(NOMOD,m) => m

  axiom merge(m,NOMOD) => m

  rule  Print.print_buf "- ignoring inner modification:\n" &
	print_mod ignmod & Print.print_buf "\n" 
	--------------------------------------
	merge(m as REDECL(_,_),ignmod) => m

  rule  Print.print_buf "# can't merge modification with a redeclaration\n"
	---------------------------------------------------------
	merge(_,REDECL(_,_)) => fail

  rule  Print.print_buf "# trying to modify final element\n"
	------------------------------------------
	merge(_,m as MOD(true,_,_)) => fail

  rule  merge_subs(subs1, subs2) => subs &
	merge_eq(ass1, ass2) => ass
	----------------------------
	merge(MOD(final,subs1,ass1), MOD(false,subs2,ass2))
	  => MOD(final,subs,ass)

  rule	Print.print_buf "# incomplete handling of modification merging\n"
	-----------------------------------------------------
	merge(outer,inner) => outer

end

(** relation: merge_subs
 ** 
 ** This relation merges to list of `SubMod's.
 **)

relation merge_subs : (SubMod list, SubMod list)
	  => SubMod list =

  axiom merge_subs(s1,[]) => s1

  rule  merge_subs2(s1,s) => (s1',s') &
	merge_subs(s1',s2) => ss
	------------------------
	merge_subs(s1,s::s2) => s'::ss

end

(** relation: merge_subs2
 ** 
 ** This relation helps in the merging of two lists of `SubMod's.  It
 ** compares one `SubMod' against a list of other `SubMod's, and if
 ** there is one with the same name,  it is kept and the one `SubMod'
 ** given in the second argument is discarded.
 **)

relation merge_subs2 : (SubMod list, SubMod)
	  => (SubMod list, SubMod) =

  axiom merge_subs2([],m) => ([],m)

	(* Modifications in the list take precedence *)

  rule  n1 = n2 & merge(m1,m2) => m
	---------------------------
	merge_subs2(NAMEMOD(n1,m1)::ss, NAMEMOD(n2,m2))
	  => (ss, NAMEMOD(n1,m))

  rule  i1 = i2 & merge(m1,m2) => m
	---------------------------
	merge_subs2(IDXMOD(i1,m1)::ss, IDXMOD(i2,m2)) => (ss, IDXMOD(i1,m))

  rule  merge_subs2(ss, s2) => (ss',s)
	------------------------------
	merge_subs2(s1::ss, s2) => (s1::ss',s)

end

(** relation: merge_eq
 ** 
 ** The outer modification, given in the first argument, takes
 ** precedence over the inner modifications.
 **)

relation merge_eq : (EqMod option, EqMod option) => EqMod option =

	(* Outer assignments take precedence *)

  axiom merge_eq(e as SOME(TYPED(_,_)), _) => e
  axiom merge_eq(e as SOME(UNTYPED(_)), _) => e
  axiom merge_eq(NONE,e) => e

end

(** relation: mod_equation
 ** 
 ** This relation simply extracts the equation part of a modification.
 **)

relation mod_equation : Mod => EqMod option =

  axiom mod_equation NOMOD => NONE
  axiom mod_equation REDECL(_,_) => NONE
  axiom mod_equation MOD(_,_,e) => e

end

(** - Printing *)
(*!ignorecode*)

(** relation: print_mod
 **
 ** This relation prints a modification. It uses a few other relation
 ** to do its stuff.
 **
 ** The relations are excluded from the report for brevity.
 **)

relation print_mod : Mod => () =

  rule	Print.print_buf "()"
	----------
	print_mod(NOMOD)

  rule  Print.print_buf "(redeclare(" &
	Dump.print_list(elist, SCode.print_element, ", ") &
	Print.print_buf "))"
	------------------------------
	print_mod REDECL(final,elist)
	
  rule	print_final final &
	print_subs1 subs &
	print_eqmod eq
	--------------
	print_mod MOD(final, subs, eq)

end

(**)

relation print_final : bool => () =

  axiom print_final false

  rule Print.print_buf " final "
       --------------
       print_final true

end

(**)

relation print_subs1 : SubMod list => () =

  axiom print_subs1 [] => ()

  rule	Print.print_buf "(" & print_sub x & print_subs2 xs & Print.print_buf ")"
	------------------------------------
	print_subs1 x::xs

end

(**)

relation print_subs2 : SubMod list => () =

  axiom print_subs2 []

  rule  Print.print_buf "," & print_sub sub & print_subs2 subs
	----------------------------------------------
	print_subs2 sub::subs

end

(**)

relation print_sub : SubMod => () =

  rule  Print.print_buf n & print_mod mod
	------------------
	print_sub NAMEMOD(n,mod)

  rule  print_subscripts ss & print_mod mod
	------------------
	print_sub IDXMOD(ss,mod)

end

(**)

relation print_subscripts : int list => () =

  rule	Print.print_buf "[]"
  	---------------
	print_subscripts []

  rule	Print.print_buf "[" &
	int_string x => s & Print.print_buf s &
	print_subscripts2 xs &
	Print.print_buf "]"
	-----------------------------------------------------
	print_subscripts x::xs

end

(**)

relation print_subscripts2 : int list => () =

  axiom	print_subscripts2 []

  rule	Print.print_buf "," & int_string x => s & Print.print_buf s &
	print_subscripts2 xs
	--------------------
	print_subscripts2 x::xs

end

(**)

relation print_eqmod : EqMod option => () =

  axiom print_eqmod NONE

  rule  Print.print_buf " =(typed) " & Exp.print_exp e
	-----------------------------
	print_eqmod SOME(TYPED(e,prop))

  rule  Print.print_buf " =(untyped) " & Dump.print_exp e
	-----------------------------
	print_eqmod SOME(UNTYPED(e))

end
	
