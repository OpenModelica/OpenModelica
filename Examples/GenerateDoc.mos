/*
 * This file is part of OpenModelica.
 *
 * Copyright (c) 1998-CurrentYear, Linköping University,
 * Department of Computer and Information Science,
 * SE-58183 Linköping, Sweden.
 *
 * All rights reserved.
 *
 * THIS PROGRAM IS PROVIDED UNDER THE TERMS OF GPL VERSION 3
 * AND THIS OSMC PUBLIC LICENSE (OSMC-PL).
 * ANY USE, REPRODUCTION OR DISTRIBUTION OF THIS PROGRAM CONSTITUTES RECIPIENT'S
 * ACCEPTANCE OF THE OSMC PUBLIC LICENSE.
 *
 * The OpenModelica software and the Open Source Modelica
 * Consortium (OSMC) Public License (OSMC-PL) are obtained
 * from Linköping University, either from the above address,
 * from the URLs: http://www.ida.liu.se/projects/OpenModelica or
 * http://www.openmodelica.org, and in the OpenModelica distribution.
 * GNU version 3 is obtained from: http://www.gnu.org/copyleft/gpl.html.
 *
 * This program is distributed WITHOUT ANY WARRANTY; without
 * even the implied warranty of  MERCHANTABILITY or FITNESS
 * FOR A PARTICULAR PURPOSE, EXCEPT AS EXPRESSLY SET FORTH
 * IN THE BY RECIPIENT SELECTED SUBSIDIARY LICENSE CONDITIONS
 * OF OSMC-PL.
 *
 * See the full OSMC Public License conditions for more details.
 *
 */

/*
 * Generates Modelica documentation automatically in about 4 minutes on Linux, 30 on Windows
 *   Includes Modelica standard libraries
 *   Most Modelica builtin operators
 *   Some non-Modelica-but-used-in-MSL operators
 *   OpenModelica scripting
 *
 * $Id$
 */

setDebugFlags("nogen") /* Don't generate dll/so-files when instantiating classes (performance) */;

loadModel(Modelica);

writeFile("loadModel.mos",sum("if loadModel(" + lib + ") then true else if loadModel(" + lib + ",languageStandard=\"2.x\") then true else loadModel(" + lib + ",languageStandard=\"1.x\");getErrorString();\n" for lib in getAvailableLibraries()));
getErrorString();
runScript("loadModel.mos");
getErrorString();
getClassNames();

// should we generate documentation for OM built-in functions?
genBuiltin := true;
// should we generate MetaModelica documentation?
genMetaModelica := true and genBuiltin;

if genMetaModelica then
  setCommandLineOptions("+g=MetaModelica");
  loadString("package MetaModelica \"MetaModelica Language Extensions\"\n" + readFile(getInstallationDirectoryPath()+"/lib/omc/MetaModelicaBuiltin.mo") + "\nend MetaModelica;","MetaModelicaBuiltin.mo");
  setCommandLineOptions("+g=Modelica");
end if;

echo(false);
self:=getInstallationDirectoryPath() + "/share/doc/omc/testmodels/GenerateDoc.mos";getErrorString();
py:=getInstallationDirectoryPath() + "/share/doc/omc/testmodels/generate_icons.py";getErrorString();
commands:={"python "+py+" --quiet --output-dir . "+typeNameString(cl) for cl in getClassNames()};getErrorString();
res:=system_parallel(commands);getErrorString();
system("rm -f *.json");getErrorString();
/* if max(res) <> 0 then
  exit(1);
end if; */

allClassNames:=getClassNames(builtin=genBuiltin,recursive=true,sort=true);
system("date -u +%FT%TZ > tmp");
genTimeStamp := readFile("tmp");
version := "<hr />Generated at " + genTimeStamp + " by <a href=\"http://openmodelica.org\">OpenModelica</a>" + getVersion() + " using <a href=\"GenerateDoc.mos\">GenerateDoc.mos</a>";

echo(true);
"classNames";
classNames := getClassNames(builtin=genBuiltin,sort=true);

"Defining functions used in this script...";
loadString("
function filename \"Replace characters that mess with filesystems and shell expansions\"
  input String str;
  output String ostr :=
        OpenModelica.Scripting.stringReplace(
          OpenModelica.Scripting.stringReplace(str,\"/\",\"Division\"),
          \"*\",\"Multiplication\")
;
end filename;

function notLast
  input String str[:];
  output String ostr[:] := str[1:end];
end notLast;

function last
  input String str[:];
  output String ostr := str[end];
end last;

function preSuffixIfNotEmpty
  input String prefix;
  input String str;
  input String suffix;
  output String out := if str <> \"\" then prefix + str + suffix else \"\";
end preSuffixIfNotEmpty;

function svgIcon
  input String file;
  input String link;
  input String cssClass;
  output String tag;
algorithm
  tag := if OpenModelica.Scripting.regularFileExists(file) then \"<a href=\\\"\" + link + \"\\\"><img class=\\\"\"+cssClass+\"\\\" src=\\\"\"+ file +\"\\\" /></a> \" else \"\";
end svgIcon;

function head
  input String strs[:];
  input String svgFile;
  output String head;
protected
  String compound := \"\", file;
algorithm
  head :=
\"<head>
  <meta http-equiv=\\\"Content-Type\\\" content=\\\"text/html; charset=utf-8\\\" />
  <link href=\\\"style.css\\\" rel=\\\"stylesheet\\\" type=\\\"text/css\\\" />
  <title>\"+sum(s + \".\" for s in strs[1:end-1])+strs[end]+\"</title>
  </head>
  <body>
  <h1>\" + svgIcon(svgFile,svgFile,\"svgiconhead\") + \"<a class=\\\"omc-h1-a\\\" href=\\\"index.html\\\">.</a>\";
  for ident in strs[1:end-1] loop
    compound := if compound == \"\" then ident else compound+\".\"+ident;
    file := filename(compound+\".html\");
    head := head + \"<a class=\\\"omc-h1-a\\\" href = \\\"\"+file+\"\\\">\"+ident+\"</a>.\";
  end for;
  head := head + \"<span class=\\\"omc-h1\\\">\" + strs[end] + \"</span></h1>\";
end head;

function itemString
  input Item item;
  input String version;
  output String res;
protected
  String docInfo, revision;
algorithm
  docInfo := if item.docInfo[1] == \"\"
    then preSuffixIfNotEmpty(\"<h2><a name=\\\"info\\\">Information</a></h2><pre>\", item.comment, \"</pre>\")
    else if OpenModelica.Scripting.regexBool(item.docInfo[1],\"<html>\") or OpenModelica.Scripting.regexBool(item.docInfo[1],\"<HTML>\")
    then \"<h2><a name=\\\"info\\\">Information</a></h2>\" + item.docInfo[1]
    else preSuffixIfNotEmpty(\"<h2><a name=\\\"info\\\">Information</a></h2><pre>\", toHtml(item.docInfo[1]), \"</pre>\");
  revision := if item.docInfo[2] <> \"\" then \"<h2><a name=\\\"revisions\\\">Revisions</a></h2>\" + item.docInfo[2] else \"\";
  res := item.head + docInfo + item.interface + item.short + item.contents + revision + version + \"</body>\";
end itemString;

function itemFile
  input Item item;
  output String file := item.file;
end itemFile;

function toHtml
  input String str;
  output String ostr := OpenModelica.Scripting.stringReplace(OpenModelica.Scripting.stringReplace(OpenModelica.Scripting.stringReplace(str,\"&\",\"&amp;\"),\"<\",\"&lt;\"),\">\",\"&gt;\");
end toHtml;

record Item
  String file;
  String head;
  String docInfo[2];
  String contents;
  String comment;
  String interface;
  String short;
end Item;
");

"Start calculate items";
echo(false);
items:={Item(
      filename(OpenModelica.Scripting.typeNameString(c))+".html",
      head(OpenModelica.Scripting.typeNameStrings(c),filename(OpenModelica.Scripting.typeNameString(c))+".svg"),
      OpenModelica.Scripting.getDocumentationAnnotation(c),
      preSuffixIfNotEmpty(
        "<h2>Contents</h2>\n<table><tr><th>Name</th><th>Description</th></tr>",
        sum("<tr><td>"+ svgIcon(filename(OpenModelica.Scripting.typeNameString(cl)) + ".svg",filename(OpenModelica.Scripting.typeNameString(cl)) + ".html","svgiconsmall") +"<a href=\"" + filename(OpenModelica.Scripting.typeNameString(cl)) + ".html\">" +
             last(OpenModelica.Scripting.typeNameStrings(cl)) + "</a></td>" + "<td>" +
             toHtml(OpenModelica.Scripting.getClassComment(cl)) +
             "</td></tr>\n"
             for cl in OpenModelica.Scripting.getClassNames(c,qualified=true,sort=false)),
        "</table>"
        ),
      toHtml(OpenModelica.Scripting.getClassComment(c)),
      preSuffixIfNotEmpty("\n<h4><a name=\"interface\">Interface</a></h4>\n<blockquote><pre>",toHtml(OpenModelica.Scripting.list(c,interfaceOnly=true)),"</pre></blockquote>"),
      preSuffixIfNotEmpty("\n<h4><a name=\"definition\">Definition</a></h4>\n<blockquote><pre>",toHtml(OpenModelica.Scripting.list(c,shortOnly=true)),"</pre></blockquote>")
    ) for c in allClassNames};
echo(true);
"Start writing items to file";
writeFile(itemFile(items) /* Vector of filenames */,itemString(items,version) /* Vector of file contents */);
getErrorString();
"Finished writing " + String(size(items,1)) + " items";
echo(false);

dirs := "";
replaceCommands := "";
filetmp := "index.html";
writeFile(filetmp, "<html><head><meta http-equiv=\"Content-Type\" content=\"text/html; charset=utf-8\" /><title>Modelica Documentation</title><link href=\"style.css\" rel=\"stylesheet\" type=\"text/css\" /></head><body><h1>Modelica Documentation</h1>\n");
writeFile(filetmp, "<p>This is a listing of builtin Modelica functions, miscellaneous Modelica libraries stored in a <a href=\"https://openmodelica.org/git/OpenModelicaLibraries.git\">git</a> repository. There is also documentation for OpenModelica-specific <a href=\"OpenModelica.Scripting.html\">scripting</a>.</p>\n", append = true);
writeFile(filetmp, "<p>Note that not all libraries are supported or have been tested. Feel free to add bug reports either to <a href=\"https://trac.openmodelica.org/OpenModelica/newticket\">OpenModelica</a> (for compiler bugs) or to the <a href=\"https://github.com/modelica-3rdparty\">GitHub projects</a> for enhancements or bugs in the libraries. Older libraries may contain broken links and images (only libraries with modelica:// links produce good documentation). The Modelica Standard Library is the best supported package in OpenModelica.</p>\n", append = true);
writeFile(filetmp, "<h2>Libraries</h2>\n", append = true);
writeFile(filetmp, "<table><tr><th>Name</th><th>Description</th><th>Version</th></tr>", append = true);
for cl in classNames loop
  file := getSourceFile(cl);
  base := basename(file);
  if base <> "ModelicaBuiltin.mo" and base <> "MetaModelicaBuiltin.mo" then
    base := basename(file);
    contentStrToks := typeNameStrings(cl);
    modelVersion := getVersion(cl);
    if base == "package.mo" then
      /* The replaceCommands are used in the Python script generated later. These are sed-style replacements with & as delimiter to avoid escaping slashes. */
      replaceCommands := replaceCommands + "\n (re.compile('^[Mm][Oo][Dd][Ee][Ll][Ii][Cc][Aa]://"+contentStrToks[end]+"/'),'"+dirname(file)+"/'),";
      dirpaths := strtok(dirname(file),"/");
      dirs := dirs + " \"" + dirpaths[end] + "\" ";
    end if;
    comment := OpenModelica.Scripting.stringReplace(getClassComment(cl),"&","&amp;"); // lets escape "&" on the fly
    contentStr := typeNameString(cl);
    fileName := filename(contentStr);
    writeFile(filetmp, "<tr><td><a href=\"" + fileName + ".html\">" + contentStrToks[end] + "</a></td><td>" + comment + "</td><td>" + modelVersion + "</td></tr>", append = true);
  end if;
end for;
writeFile(filetmp, "</table>\n", append = true);

if genBuiltin then
  writeFile(filetmp, "<h2>Builtin Environment</h2>\n", append = true);
  writeFile(filetmp, "<table><tr><th>Name</th><th>Description</th><th>Version</th></tr>", append = true);
  for cl in classNames loop
    file := getSourceFile(cl);
    base := basename(file);
    if base == "ModelicaBuiltin.mo" or base == "MetaModelicaBuiltin.mo" then
      contentStr := typeNameString(cl);
      contentStrToks := typeNameStrings(cl);
      modelVersion := getVersion(cl);
      comment := OpenModelica.Scripting.stringReplace(getClassComment(cl),"&","&amp;"); // lets escape "&" on the fly
      fileName := filename(contentStr);
      writeFile(filetmp, "<tr><td><a href=\"" + fileName + ".html\">" + contentStrToks[end] + "</a></td><td>" + comment + "</td><td>" + modelVersion + "</td></tr>", append = true);
    end if;
  end for;
  writeFile(filetmp, "</table>\n", append = true);
end if;
writeFile(filetmp, version + " (<a href=\"ModelicaDocumentation.tar.xz\">Offline version</a>)", append = true);
writeFile(filetmp, "</body>\n</html>", append = true);

writeFile("Tidy.py","#!/usr/bin/env python
# -*- coding: utf-8 -*-
from __future__ import with_statement
from BeautifulSoup import BeautifulSoup
import subprocess as sub
import re
import glob
import sys

repls = ["+replaceCommands+"
 (re.compile(r'.*/omlibrary/'), ''),
 (re.compile(r' '), r'%20'),
 (re.compile(r'[Mm][Oo][Dd][Ee][Ll][Ii][Cc][Aa]://([A-Za-z0-9.\\'()_]*)#'), r'\\1.html#'),
 (re.compile(r'[Mm][Oo][Dd][Ee][Ll][Ii][Cc][Aa]://([A-Za-z0-9.\\'()_]*)'), r'\\1.html'),
]
rehttp = re.compile(r'https?://',flags=re.IGNORECASE)
ignorebookmark = re.compile(r'^([^#]*)#.*')
links = open('tidy.links','w')

def linkreplace(link,filepath):
  result = link
  for (regex,repl) in repls:
    try:
      result = regex.sub(repl,result)
    except:
      pass
  if result <> link and not rehttp.match(result):
    links.write('%s!%s\\n' % (filepath,ignorebookmark.sub(r'\\1',result)))
  return result

for filepath in sorted(glob.glob('*.html')):
  tag = '[Checking file %s]:\\n' % filepath
#  sys.stdout.write(tag)  # not much point in writing the tags to stdout
  sys.stderr.write(tag)
  pid = sub.call(['tidy', '-utf8', '-modify', '-asxhtml', '--add-xml-decl', 'yes', '-quiet', filepath])
  if pid <> 2:
    with open(filepath,'r') as html_file:
      soup = BeautifulSoup(html_file, fromEncoding='utf-8')
      for a in soup.findAll('a'):
        try:
          a['href'] = linkreplace(a['href'],filepath)
        except:
          pass
        for img in soup.findAll('img'):
          try:
            img['src'] = linkreplace(img['src'],filepath)
          except:
            pass
    with open(filepath,'w') as html_file:
      html_file.write(str(soup))
  else:
    print('Tidy failed with %s, skipping link-replacement for %s!'  % (pid,filepath))
");

writeFile("FindFiles.sh","#!/bin/bash
rm -f *.png *.pdf FindFiles.log
touch FindFiles.log
OMLIBRARY=\""+getInstallationDirectoryPath()+"/lib/omlibrary\"
for f in `sort -u tidy.links`; do
  link=`echo $f | cut '-d!' -f2- | sed 's/%20/ /'`
  if test -f \"$OMLIBRARY/$link\"; then
    d=`dirname \"$link\"`
    mkdir -p \"$d\"
    cp \"$OMLIBRARY/$link\" \"$link\"
  elif test -f \"$link\"; then
    true
  else
    inFile=`echo $f | cut '-d!' -f1`
    echo \"$inFile: Not found: $link\" | tee -a FindFiles.log
  fi
done
rm -f tmp
");

writeFile("style.css",".omc-h1
{
    font-family: mono;
    font-size: x-large;
    color:rgb(153,0,0);
    vertical-align: middle;
}

.omc-h1-a
{
    font-family: mono;
    font-size: large;
    vertical-align: middle;
}

table
{
    border-color: black;
    border-width: 1px 1px 1px 1px;
    border-style: solid;
    border-spacing: 0;
    border-collapse: collapse;
}

table td,th
{
    border-color: black;
    border-width: 1px 1px 1px 1px;
    border-style: solid;
    margin: 0;
    padding: 4px;
}

pre
{
    white-space: pre-wrap; /* CSS 3 */
}

.svgiconhead {
  height: 32px;
  width: 32px;
  vertical-align: top;
  -o-object-fit: contain;
}
.svgiconsmall {
  height: 20px;
  width: 20px;
  vertical-align: top;
  -o-object-fit: contain;
}
");

writeFile("FilterTidy.sh","#!/bin/bash
grep -v DOCTYPE tidy.err | grep -v \"<body>\" |grep -v \"<html>\\|</html>\" |grep -v lacks |grep -v apos | grep -B1 Warning > tidy.filtered
");

echo(true);
"Removing previous generation output files...";
system("rm -f tidy.links tidy.out tidy.err tidy.filtered");

"Copying original HTML files to 'old-html-tmp'";
system("mkdir -p old-html-tmp");
system("cp *.html old-html-tmp/");

"Tidy.py";
system("python Tidy.py 2> tidy.err");

"FilterTidy.sh";
system("bash FilterTidy.sh");

"FindFiles.sh";
system("bash FindFiles.sh");
system("rm -f ModelicaDocumentation.tar.xz");
system("mkdir -p " + dirs);
system("cp '" + py + "' '" + self + "' .");
"tar";
dirs;
cmd := "tar cJf ModelicaDocumentation.tar.xz --dereference style.css *.html " + dirs + " GenerateDoc.mos generate_icons.py FindFiles.log tidy.filtered tidy.links";
system(cmd);
getErrorString();
